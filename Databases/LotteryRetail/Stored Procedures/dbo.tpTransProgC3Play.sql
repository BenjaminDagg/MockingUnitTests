SET QUOTED_IDENTIFIER ON
GO
SET ANSI_NULLS ON
GO
/*
--------------------------------------------------------------------------------
User Stored Procedure tpTransProgPlay

Created 07-12-2010 by Terry Watkins

Desc: Handles Class 3 Progressive Play transactions.

Return values: ErrorCode, ErrorDescription, ShutdownFlag

Called by: Transaction Portal

Parameters:
   @MachineNumber       The Machine number played
   @TimeStamp           DateTime from the machine (overwritten by this procedure).
   @MachineSequence     Transaction sequence number generated by the TransPortal
   @CoinsBet            Number of Coins Bet
   @CoinsWon            Number of Coins Won
   @LinesBet            Number of Lines Bet
   @MachineBalance      What the machine sent as it's new balance
   @MachineDenomination Denomination of the Play
   @PressUpCount        Number of times player pressed up their bet (for the Press It Up Poker game)
   @PromoBalance        Promotional Balance in cents (default = 0)
   @ProgressiveAmount   Progressive Win Amount in cents (default = 0)
   @TransID             Identifies the transaction (10 = Loss, 11 = Win, 12 = Jackpot, 13 = Forfeit)

Change Log:
Changed By    Date           Database Version
  Change Description
--------------------------------------------------------------------------------
Terry Watkins 07-12-2010     v7.2.2
  Initial coding

Terry Watkins 11-11-2010     DCLottery v1.0.0
  Added retrieval of @LocationID to support addition of column
  CASINO_TRANS.LOCATION_ID
  
Louis Epstein 04-10-2014     v3.2.1
  Added functionality to contribute to multi level progressive pools
  
Louis Epstein 06-18-2014     v3.2.3
  Modified stat recording functionality to use tpUpdatePlayStatsHourlyAndDaily
--------------------------------------------------------------------------------
*/
CREATE PROCEDURE [dbo].[tpTransProgC3Play]
   @CoinsBet             Int,
   @CoinsWon             Int,
   @LinesBet             Int,
   @MachineBalance       Int,
   @MachineDenomination  Int,
   @MachineNumber        VarChar(5),
   @MachineSequence      Int,
   @TimeStamp            DateTime,
   @PressUpCount         SmallInt,
   @PromoBalance         Int = 0,
   @ProgressiveAmount    Int = 0,
   @TransID              SmallInt
AS

-- Variable Declarations
DECLARE @AcctDate           DateTime
DECLARE @BaseTrans          Money
DECLARE @CasinoMachNo       VarChar(8)
DECLARE @CTTransAmount      Money
DECLARE @DealNumber         Int
DECLARE @DealTypeCode       Char(1)
DECLARE @Debug              Bit
DECLARE @ErrorID            Int
DECLARE @ErrorSource        VarChar(64)
DECLARE @ErrorDescription   VarChar(256)
DECLARE @EventCode          VarChar(2)
DECLARE @EventLogDesc       VarChar(1024)
DECLARE @GameTypeCode       VarChar(2)
DECLARE @LocationID         Int
DECLARE @LockupMachine      Int
DECLARE @MachineBal         Money
DECLARE @MachinePromoBal    Money
DECLARE @MachGameCode       Char(3)
DECLARE @MachNbrAsInt       Int
DECLARE @MachineDenom       Money
DECLARE @MsgText            VarChar(3072)
DECLARE @PRate1             Decimal(9,8)
DECLARE @PRate2             Decimal(9,8)
DECLARE @PRate3             Decimal(9,8)
DECLARE @PRateCombined      Decimal(9,8)
DECLARE @ProductID          TinyInt
DECLARE @ProductLineID      SmallInt
DECLARE @ProgContribution   Money
DECLARE @ProgressiveAmt     Money
DECLARE @ProgressiveTypeID  Int
DECLARE @SummarizePlay      Bit
DECLARE @TabCost            SmallMoney
DECLARE @TotalCoinsBet      Int
DECLARE @TransAmt           Money

SET NOCOUNT ON

-- Variable Initialization

-- Convert balance and denom first, in case other vars depend on them...
SET @MachineBal         = CAST(@MachineBalance AS Money) / 100
SET @MachineDenom       = CONVERT(Money, @MachineDenomination) / 100
SET @MachinePromoBal    = CAST(@PromoBalance AS MONEY) / 100
SET @ProgressiveAmt     = CAST(@ProgressiveAmount AS Money) / 100

SET @CTTransAmount      = 0
SET @DealNumber         = 0
SET @DealTypeCode       = ' '
SET @Debug              = 0
SET @ErrorID            = 0
SET @ErrorDescription   = ''
SET @ErrorSource        = 'tpTransProgC3Play Stored Procedure'
SET @EventCode          = 'SP'
SET @GameTypeCode       = ''
SET @EventLogDesc       = ''
SET @LocationID         = 0
SET @LockupMachine      = 0
SET @MachNbrAsInt       = CAST(@MachineNumber AS Int)
SET @PRate1             = 0
SET @PRate2             = 0
SET @PRate3             = 0
SET @PRateCombined      = 0
SET @ProductLineID      = 0
SET @ProgContribution   = 0
SET @ProgressiveTypeID  = 0
SET @TotalCoinsBet      = @CoinsBet
SET @TransAmt           = 0
SET @TimeStamp          = GetDate()

-- Check Debug mode...
IF EXISTS(SELECT * FROM DEBUG_SETTING WHERE DEBUG_ENTITY = 'tpTransProgC3Play')
   BEGIN
      SELECT @Debug = DEBUG_MODE FROM DEBUG_SETTING WHERE DEBUG_ENTITY = 'tpTransProgC3Play'
   END

-- If Debug mode, save argument values...
IF (@Debug = 1)
   -- Record argument values.
   BEGIN
      SET @MsgText =
         'tpTransProgC3Play Argument Values:  ' + 
         '  MachineNumber: '       + ISNULL(@MachineNumber, '<null>') +
         '  MachineSequence: '     + ISNULL(CAST(@MachineSequence AS VarChar), '<null>') +
         '  CoinsBet: '            + ISNULL(CAST(@CoinsBet AS VarChar), '<null>') +
         '  CoinsWon: '            + ISNULL(CAST(@CoinsWon AS VarChar), '<null>') +
         '  LinesBet: '            + ISNULL(CAST(@LinesBet AS VarChar), '<null>') +
         '  MachineBalance: '      + ISNULL(CAST(@MachineBalance AS VarChar), '<null>') +
         '  MachineDenomination: ' + ISNULL(CAST(@MachineDenomination AS VarChar), '<null>') +
         '  PressUpCount: '        + ISNULL(CAST(@PressUpCount AS VarChar), '<null>') +
         '  PromoBalance: '        + ISNULL(CAST(@PromoBalance AS VarChar), '<null>') +
         '  ProgressiveAmount: '   + ISNULL(CAST(@ProgressiveAmount AS VarChar), '<null>') +
         '  TransID: '             + ISNULL(CAST(@TransID AS VarChar), '<null>')
      
      EXEC InsertDebugInfo @TransID, @MsgText, @MachNbrAsInt
   END

-- Retrieve CASINO information.
SELECT @SummarizePlay = SUMMARIZE_PLAY, @LocationID = LOCATION_ID FROM CASINO WHERE SETASDEFAULT = 1

-- Get Current Accounting Date.
SET @AcctDate = dbo.ufnGetAcctDate()

-- If the MACH_SETUP row was found, retrieve Machine information.
IF EXISTS(SELECT * FROM dbo.MACH_SETUP WHERE MACH_NO = @MachineNumber)
   BEGIN
      SELECT
         @CasinoMachNo      = ms.CASINO_MACH_NO,
         @MachGameCode      = ms.GAME_CODE,
         @GameTypeCode      = gt.GAME_TYPE_CODE,
         @DealTypeCode      = ISNULL(gt.TYPE_ID, ' '),
         @ProgressiveTypeID = gt.PROGRESSIVE_TYPE_ID,
         @ProductID         = gt.PRODUCT_ID,
         @ProductLineID     = pl.PRODUCT_LINE_ID
      FROM MACH_SETUP ms
         JOIN BANK          b ON ms.BANK_NO = b.BANK_NO
         JOIN PRODUCT_LINE pl ON b.PRODUCT_LINE_ID = pl.PRODUCT_LINE_ID
         JOIN GAME_TYPE    gt ON b.GAME_TYPE_CODE = gt.GAME_TYPE_CODE
      WHERE MACH_NO = @MachineNumber
      
      -- Test for Product ID 6.
      IF (@ProductID <> 6)
         SET @ErrorID = 341
      
      -- Test for Product Line ID 11.
      IF (@ProductLineID <> 11)
         SET @ErrorID = 338
   END
ELSE
   BEGIN
      -- Set Invalid Machine error.
      SET @ErrorID = 104
   END

-- Is the Transaction identifier good?
IF (@ErrorID = 0)
   BEGIN
      IF @TransID NOT BETWEEN 10 AND 13
         SET @ErrorID = 342
   END

-- Is this a progressive game?
IF (@ErrorID = 0)
   BEGIN
      IF (@ProgressiveTypeID > 0)
         -- Yes, so get progressive info...
         BEGIN
            -- Retrieve ProgressiveType data...
            SELECT
@PRateCombined = SUM(Rate1) + SUM(Rate2) + SUM(Rate3)
FROM PROGRESSIVE_POOL
WHERE PROGRESSIVE_TYPE_ID = @ProgressiveTypeID AND DENOMINATION = @MachineDenomination
GROUP BY PROGRESSIVE_TYPE_ID, DENOMINATION
         END
      ELSE
         -- No, so set error number...
         BEGIN
            SET @ErrorID = 339
         END
   END

IF (@ErrorID = 0) 
   BEGIN
      IF @DealTypeCode <> 'K'
         -- Non-Keno game.
         BEGIN
            -- Set Transaction amount variables, Non-Keno style...
            SET @BaseTrans = @MachineDenom * @CoinsWon
            SET @TabCost = @MachineDenom * @CoinsBet * @LinesBet
            SET @TotalCoinsBet = @CoinsBet
            IF (@PressUpCount > 0) 
               BEGIN
                  SET @TabCost = (@TabCost * (@PressUpCount + 1))
                  SET @TotalCoinsBet = (@TotalCoinsBet * (@PressUpCount + 1))
               END
         END
      ELSE
         -- Keno game.
         BEGIN
            -- Set Transaction amount variables, Keno style...
            SET @BaseTrans = CAST(@CoinsWon AS MONEY) / 100
            SET @TabCost = @MachineDenom * @CoinsBet
         END
      
      -- Store the total transaction amount.
      SET @CTTransAmount = @BaseTrans + @ProgressiveAmt
      
      -- Record the Balance, PromoBalance and current time (as LAST_ACTIVITY) for the machine in MACH_SETUP.
      UPDATE MACH_SETUP SET
         BALANCE       = @MachineBal,
         PROMO_BALANCE = @MachinePromoBal,
         LAST_ACTIVITY = @TimeStamp
      WHERE MACH_NO = @MachineNumber
      
      -- Calculate the progressive contribution.
      SET @ProgContribution = (@TabCost * @PRateCombined) / 100
      
      -- Debug mode?
      IF (@Debug = 1)
         -- Yes, so log progressive information...
         BEGIN
            SET @MsgText =
               'tpTransProgC3Play - ProgressiveTypeID: '
                                      + ISNULL(CAST(@ProgressiveTypeID AS VarChar), '<null>') +
               '  ProgRate1: '        + ISNULL(CAST(@PRate1 AS VarChar), '<null>') +
               '  ProgRate2: '        + ISNULL(CAST(@PRate2 AS VarChar), '<null>') +
               '  ProgRate3: '        + ISNULL(CAST(@PRate3 AS VarChar), '<null>') +
               '  ProgContribution: ' + ISNULL(CONVERT(VarChar, @ProgContribution, 2), '<null>') +
               '  TabCost: '          + ISNULL(CONVERT(VarChar, @TabCost, 2), '<null>') +
               '  TotalCoinsBet: '    + ISNULL(CAST(@TotalCoinsBet AS VarChar), '<null>') +
               '  BaseTrans: '        + ISNULL(CONVERT(VarChar, @BaseTrans, 2), '<null>') +
               '  CTTransAmount: '    + ISNULL(CONVERT(VarChar, @CTTransAmount, 2), '<null>') +
               '  GameCode: '         + ISNULL(@MachGameCode, '<null>') +
               '  GameTypeCode: '     + ISNULL(@GameTypeCode, '<null>')
            
            EXEC InsertDebugInfo @TransID, @MsgText, @MachNbrAsInt
         END
      
      -- Retrieve the Progressive Pool ID.
      IF EXISTS(SELECT * FROM dbo.PROGRESSIVE_POOL
                WHERE
                   PROGRESSIVE_TYPE_ID = @ProgressiveTypeID AND
                   GAME_TYPE_CODE = @GameTypeCode           AND
                   DENOMINATION = @MachineDenomination)
         BEGIN
            EXEC UpdateProgressivePools @ProgressiveTypeID, @AcctDate, @GameTypeCode, @MachineDenomination, @TabCost
         END
      ELSE
         BEGIN
            -- Progressive Pool not found.
            SET @ErrorID = 340
         END      
   END

-- Handle any errors
IF (@ErrorID <> 0)
   BEGIN
      SELECT
         @ErrorDescription = ISNULL([DESCRIPTION], 'Undefined Error ' + CAST(@ErrorID AS VarChar)),
         @LockupMachine    = ISNULL(LOCKUP_MACHINE, 1)
      FROM ERROR_LOOKUP
      WHERE ERROR_NO = @ErrorID
      
      -- Build Error Message
      SET @EventLogDesc = 'Description: '             + @ErrorDescription +
                          ', Machine Number: '        + @MachineNumber    +
                          ', Casino Machine Number: ' + ISNULL(@CasinoMachNo, '<null>')
      
      -- Test if the Machine should be loocked up.
      IF (@LockupMachine <> 0)
         BEGIN
            UPDATE MACH_SETUP SET ACTIVE_FLAG = 0 WHERE MACH_NO = @MachineNumber AND ACTIVE_FLAG = 1
            SET @EventCode = 'SD'
         END
      
      -- Insert Error into the Casino_Event_Log
      INSERT INTO CASINO_EVENT_LOG
         (EVENT_CODE, EVENT_SOURCE, EVENT_DESC, ERROR_NO, MACH_NO)
      VALUES
         (@EventCode, @ErrorSource, @EventLogDesc, @ErrorID, @MachineNumber)
      
      -- Update Machine_Last_Play
      UPDATE MACH_LAST_PLAY SET ERROR_NO = @ErrorID WHERE MACH_NO = @MachineNumber

   END

-- If not locking up the machine, update summary tables...
IF (@LockupMachine = 0) 
   BEGIN      
      
     DECLARE @MachineDenomCast smallmoney, @CoinsBetCast smallint, @LinesBetCast smallint
  SET @MachineDenomCast = CAST(@MachineDenom AS SmallMoney)
  SET @CoinsBetCast = CAST(@CoinsBet AS SmallInt)
  SET @LinesBetCast = CAST(@LinesBet AS SmallInt)
     
     
            IF (@TransID = 10)
               -- Losing Play
               BEGIN
      
  EXEC tpUpdatePlayStatsHourlyAndDaily 
  @LocationIdPass = @LocationID,
@TimeStampPass = @TimeStamp,
@DenomPass = @MachineDenomCast,
@CoinsBetPass = @CoinsBetCast,
@LinesBetPass = @LinesBetCast,
@MachNoPass = @MachineNumber,
@CasinoMachNoPass = @CasinoMachNo,
@AcctDatePass = @AcctDate,
@DealNoPass = @DealNumber,
@GameCodePass = @MachGameCode,
@PlayCountPass = 1,
@AmountPlayedPass = @TabCost,
@LossCountPass = 1,
@AmountLostPass = @TabCost,
@ProgContributionPass = @ProgContribution

               END
            ELSE IF (@TransID = 11)
               -- Winning Play
               BEGIN
                  
                EXEC tpUpdatePlayStatsHourlyAndDaily 
                @LocationIdPass = @LocationID,
@TimeStampPass = @TimeStamp,
@DenomPass = @MachineDenomCast,
@CoinsBetPass = @CoinsBetCast,
@LinesBetPass = @LinesBetCast,
@MachNoPass = @MachineNumber,
@CasinoMachNoPass = @CasinoMachNo,
@AcctDatePass = @AcctDate,
@DealNoPass = @DealNumber,
@GameCodePass = @MachGameCode,
@PlayCountPass = 1,
@AmountPlayedPass = @TabCost,
@WinCountPass = 1,
@AmountWonPass = @CTTransAmount,
@ProgContributionPass = @ProgContribution

                  
               END
            ELSE IF (@TransID = 12)
               -- Jackpot Play
               BEGIN
                  
                EXEC tpUpdatePlayStatsHourlyAndDaily 
                @LocationIdPass = @LocationID,
@TimeStampPass = @TimeStamp,
@DenomPass = @MachineDenomCast,
@CoinsBetPass = @CoinsBetCast,
@LinesBetPass = @LinesBetCast,
@MachNoPass = @MachineNumber,
@CasinoMachNoPass = @CasinoMachNo,
@AcctDatePass = @AcctDate,
@DealNoPass = @DealNumber,
@GameCodePass = @MachGameCode,
@PlayCountPass = 1,
@JackpotCountPass = 1,
@AmountPlayedPass = @TabCost,
@AmountJackpotPass = @CTTransAmount,
@AmountProgPass = @ProgressiveAmt


                  
               END
            ELSE IF (@TransID = 13)
               -- Forfeited Play
               BEGIN
                  
                EXEC tpUpdatePlayStatsHourlyAndDaily 
                @LocationIdPass = @LocationID,
@TimeStampPass = @TimeStamp,
@DenomPass = @MachineDenomCast,
@CoinsBetPass = @CoinsBetCast,
@LinesBetPass = @LinesBetCast,
@MachNoPass = @MachineNumber,
@CasinoMachNoPass = @CasinoMachNo,
@AcctDatePass = @AcctDate,
@DealNoPass = @DealNumber,
@GameCodePass = @MachGameCode,
@PlayCountPass = 1,
@ForfeitCountPass = 1,
@AmountPlayedPass = @TabCost,
@AmountForfeitedPass = @CTTransAmount,
@ProgContributionPass = @ProgContribution

                  
               END
      
      -- Update MACHINE_PLAY_STATS table...
      IF (@SummarizePlay = 1) 
         EXEC tpUpdateMachinePlayStats
            @MachineNumber    = @MachineNumber,
            @CasinoMachineNbr = @CasinoMachNo,
            @DealNo           = @DealNumber,
            @GameCode         = @MachGameCode,
            @Denom            = @MachineDenom,
            @CoinsBet         = @TotalCoinsBet,
            @LinesBet         = @LinesBet,
            @AmountPlayed     = @TabCost,
            @AmountWon        = @CTTransAmount,
            @AcctDate         = @AcctDate,
            @LocationID       = @LocationID
      
      -- Update the MACHINE_METER table...
      IF EXISTS (SELECT * FROM dbo.MACHINE_METER WHERE MACH_NO = @MachineNumber)
         -- Update MACHINE_METER table...
         BEGIN
            UPDATE MACHINE_METER SET
               AMOUNT_PLAYED     = AMOUNT_PLAYED     + @TabCost,
               AMOUNT_WON        = AMOUNT_WON        + @CTTransAmount,
               PLAY_COUNT        = PLAY_COUNT        + 1,
               PROG_CONTRIBUTION = PROG_CONTRIBUTION + @ProgContribution
            WHERE
               MACH_NO = @MachineNumber
         END
      ELSE
         -- The record doesn't exist - so create it.
         BEGIN
            INSERT INTO MACHINE_METER
               (MACH_NO, AMOUNT_PLAYED, AMOUNT_WON, PLAY_COUNT, PROG_CONTRIBUTION)
            VALUES
               (@MachineNumber, @TabCost, @CTTransAmount, 1, @ProgContribution)
         END
   END

-- If debugging, save return values...
IF (@Debug = 1)
   BEGIN
      SET @MsgText =
         'tpTransProgC3Play Return Values - ErrorID: ' + CAST(@ErrorID AS VarChar) +
         '  ErrorDescription: ' + @ErrorDescription +
         '  LockupMachine: ' + CAST(@LockupMachine AS VarChar)
      
      EXEC InsertDebugInfo @TransID, @MsgText, @MachNbrAsInt
   END

-- Return results to client
SELECT
   @ErrorID          AS ErrorID,
   @ErrorDescription AS ErrorDescription,
   @LockupMachine    AS ShutDownFlag
GO
