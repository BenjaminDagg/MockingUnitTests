SET QUOTED_IDENTIFIER ON
GO
SET ANSI_NULLS ON
GO
/*
--------------------------------------------------------------------------------
User Stored Procedure tpTransE

Created 04-11-2003 by Chris Coddington

Desc: Handles all extract requests.

Return values: Varies...

Called by: Transaction Portal

Parameters: Many...
   @CardAccount         Card Account Number
   @MachineBalance      Balance sent by the Machine in cents
   @MachineDenomination Machine Denomination in cents
   @MachineNumber       Machine Number (identifier)
   @MachineSequence     Sequence Number generated by the Machine
   @TimeStamp           Date and Time from the Machine (but overwritten here)
   @PromoBalance        Promotional Balance from the Machine

Change Log:

Changed By    Date           Database Version
  Change Description
--------------------------------------------------------------------------------
Chris C.      07-25-2003
  Removed machine denomination validation.

Chris C.      02-10-2004
  Changed float datatypes to money datatypes.

Terry Watkins 03-01-2004
  Added retrieval of MACH_SETUP.GAME_CODE for insertion into CASINO_TRANS.

Terry Watkins 11-01-2004     v4.0.0
  Added insert of TRANS_ID / removed insert of TRANS_TYPE into CASINO_TRANS.
  Added support for Cardless game play.

Terry Watkins 05-06-2005     v4.1.1
  Added argument @PromoBalance to support the MGAM TPI.

Terry Watkins 06-15-2005     v4.1.4
  Changed size of @CardAccount from 16 to 20.

A. Murthy     12-23-2005     v5.0.1
  Added new var. @IsTechCard to prevent Balance Checking when a Tech card is extracted
  during MGAM card play.

Terry Watkins 01-16-2006     v5.0.1
  Modified update to MACH_SETUP.ACTIVE_FLAG so that on error, the flag is set
  to 0 only if it is currently set to 1 (data type changed from Bit to TinyInt).

Terry Watkins 10-04-2006     v5.0.8
  Removed @TicketStatusID. No longer needed because column
  CASINO_TRANS.TICKET_STATUS_ID has been removed.

Terry Watkins 07-29-2008     v6.0.2
  Added Tech Key support.

Terry Watkins 11-03-2009     v7.0.0
  Uses new function dbo.ufnGetAcctDate() to retrieve the accounting date.
  @AcctDate changed from VarChar(16) to DateTime.

Terry Watkins 05-12-2010     v7.2.1
  Modified insert into CASINO_EVENT_LOG to update new columns ERROR_NO and
  MACH_NO.

Terry Watkins 11-11-2010     DCLottery v1.0.0
  Added retrieval of @LocationID to support addition of column
  CASINO_TRANS.LOCATION_ID
  
Louis Epstein 10-25-2013     v3.1.6
  Added machine timestamp functionality
--------------------------------------------------------------------------------
*/
CREATE Procedure [dbo].[tpTransE]
   @CardAccount         VarChar(20),
   @MachineBalance      Int,
   @MachineDenomination Int,
   @MachineNumber       VarChar(5),
   @MachineSequence     Int,
   @TimeStamp           DateTime,
   @PromoBalance        Int = 0
AS

-- Variable Declarations
DECLARE @AcctDate         DateTime
DECLARE @AccountPrefix    VarChar(6)
DECLARE @Balance          Money
DECLARE @Barcode          VarChar(128)
DECLARE @CardRequired     Bit
DECLARE @CasinoID         VarChar(6)
DECLARE @CasinoTransID    Int
DECLARE @CoinsBet         Int
DECLARE @DealNo           Int
DECLARE @Debug            Bit
DECLARE @Denomination     Money
DECLARE @ErrorID          Int
DECLARE @ErrorSource      VarChar(64)
DECLARE @ErrorText        VarChar(1024)
DECLARE @ErrorDescription VarChar(256)
DECLARE @EventCode        VarChar(2)
DECLARE @EventLogDesc     VarChar(1024)
DECLARE @IsActive         Bit
DECLARE @IsTechCard       Bit
DECLARE @IsTechKey        Bit
DECLARE @LinesBet         Int
DECLARE @LocationID       Int
DECLARE @LockupMachine    Int
DECLARE @MachineBal       Money
DECLARE @MachineDenom     Money
DECLARE @MachNbrAsInt     Int
DECLARE @MachNo           VarChar(5)
DECLARE @MachGameCode     VarChar(3)
DECLARE @MsgText          VarChar(2048)
DECLARE @RollNo           Int
DECLARE @TicketNo         Int
DECLARE @TierLevel        SmallInt
DECLARE @ToTime           DateTime
DECLARE @TransAmt         Money
DECLARE @TransID          SmallInt
DECLARE @UAccount         VarChar(10)
DECLARE @MachineTimeStamp DateTime

SET NOCOUNT ON

-- Variable Initialization
SET @MachineTimeStamp = @TimeStamp
SET @AccountPrefix    = LEFT(@CardAccount, 6)
SET @Balance          = 0
SET @Barcode          = ''
SET @CardRequired     = 1
SET @CoinsBet         = 0
SET @Denomination     = 0
SET @DealNo           = 0
SET @ErrorID          = 0
SET @ErrorDescription = ''
SET @ErrorSource      = 'tpTransE Stored Procedure'
SET @ErrorText        = ''
SET @EventCode        = 'SP'
SET @EventLogDesc     = ''
SET @IsActive         = 0
SET @IsTechCard       = 0
SET @IsTechKey        = 0
SET @LinesBet         = 0
SET @LocationID       = 0
SET @LockupMachine    = 0
SET @MachineBal       = CONVERT(Money, @MachineBalance) / 100
SET @MachineDenom     = CONVERT(Money, @MachineDenomination) / 100
SET @MachNbrAsInt     = CAST(@MachineNumber AS Int)
SET @RollNo           = 0
SET @TicketNo         = 0
SET @TierLevel        = 0
SET @TransAmt         = 0
SET @TransID          = 41
SET @TimeStamp        = GetDate()
SET @UAccount         = RIGHT(@CardAccount, 10)


-- Check Debug mode...
SELECT @Debug = ISNULL(DEBUG_MODE, 0) FROM DEBUG_SETTING WHERE DEBUG_ENTITY = 'tpTransE'

-- Are we in Debug mode?
IF (@Debug = 1)
   -- Yes, so record incoming argument values...
   BEGIN
      SET @MsgText =
         'tpTransE Argument Values:  ' + 
         '  CardAccount: '         + ISNULL(@CardAccount, '<null>') +
         '  MachineBalance: '      + ISNULL(CAST(@MachineBalance AS VarChar), '<null>') +
         '  MachineDenomination: ' + ISNULL(CAST(@MachineDenomination AS VarChar), '<null>') +         '  MachineNumber: '       + ISNULL(@MachineNumber, '<null>') +
         '  MachineSequence: '     + ISNULL(CAST(@MachineSequence AS VarChar), '<null>') +
         '  TimeStamp: '           + ISNULL(CAST(@TimeStamp AS VarChar), '<null>') +
         '  PromoBalance: '        + ISNULL(CAST(@PromoBalance AS VarChar), '<null>')
      
      EXEC InsertDebugInfo @TransID, @MsgText, @MachNbrAsInt
   END

-- Is the Card a Tech Card?
IF (@AccountPrefix = 'DGE001') SET @IsTechCard = 1

-- Is the Card really a Tech key?
IF (@IsTechCard = 1 AND @UAccount = '77777OpKey') SET @IsTechKey = 1

-- Retrieve CASINO information.
SELECT
   @CasinoID     = CAS_ID,
   @ToTime       = TO_TIME,
   @CardRequired = PLAYER_CARD,
   @LocationID   = LOCATION_ID
FROM CASINO
WHERE SETASDEFAULT = 1

-- Get Current Accounting Date.
SET @AcctDate = dbo.ufnGetAcctDate()

-- Update MACH_SETUP.LAST_ACTIVITY and retrive GAME_CODE for the machine.
UPDATE MACH_SETUP SET
   LAST_ACTIVITY = @TimeStamp,
   @MachGameCode = GAME_CODE
WHERE MACH_NO = @MachineNumber

-- Did we find the MACH_SETUP row
IF (@@ROWCOUNT = 0)
   SET @ErrorID = 104

-- Are we still error free?
IF (@ErrorID = 0)
   BEGIN
      -- Yes, is a card required to for game play?
      IF (@CardRequired = 1)
         BEGIN
            -- Yes, so retrieve machine number and balance from CARD_ACCT.
            SELECT @MachNo = MACH_NO, @Balance = BALANCE
            FROM CARD_ACCT
            WHERE CARD_ACCT_NO = @CardAccount
            
            -- Test that the card exists.
            IF (@@ROWCOUNT > 0)
               BEGIN
                  -- Test that the card is in the machine.
                  IF (@MachNo = @MachineNumber)
                     BEGIN
                        -- If not a Tech Key, remove Machine number reference
                        -- and Session Date for the Card Account row.
                        IF (@IsTechKey = 0)
                           UPDATE CARD_ACCT SET
                              MACH_NO      = '0',
                              SESSION_DATE = NULL
                           WHERE CARD_ACCT_NO = @CardAccount 
                        
                        -- Do the Balance check only if it is a player card.
                        IF ((@Balance <> @MachineBal) AND (@IsTechCard = 0))
                        -- Balance is off.
                           BEGIN
                              -- 120 locks machine, 105 does not.
                              SET @ErrorID = 120
                              -- In either case, error text is the same...
                              SET @ErrorText = ',Machine balance should be: ' +
                                 CONVERT(VarChar(16), @Balance) +
                                 ', balance submitted was: ' + CONVERT(VarChar(16), @MachineBal)
                           END
                     END
                  ELSE
                     -- Card is not in the machine.
                     SET @ErrorID = 108
               END
            ELSE
               -- Card was not found in the CARD_ACCT table.
               SET @ErrorID = 102
         END
      ELSE
         BEGIN
            -- No, Cardless Play is in effect.
            -- Retrieve the Balance from the MACH_SETUP table.            SELECT @Balance = BALANCE FROM MACH_SETUP WHERE MACH_NO = @MachineNumber
            
            -- If not invoked by TechKey, remove Machine number
            -- and Session Date from the Card Account row...
            IF (@IsTechKey = 0)
               UPDATE CARD_ACCT SET
                  MACH_NO      = '0',
                  SESSION_DATE = NULL
               WHERE CARD_ACCT_NO = @CardAccount 
         END
   END

-- Handle any errors
IF (@ErrorID <> 0)
   BEGIN
      SELECT
         @ErrorDescription = ISNULL([DESCRIPTION], 'Undefined Error ' + CAST(@ErrorID AS VarChar(10))),
         @LockupMachine    = ISNULL(LOCKUP_MACHINE, 1)
      FROM ERROR_LOOKUP
      WHERE ERROR_NO = @ErrorID
      
      -- If in Cardless Play mode, reset error text.
      IF (@CardRequired = 0)
         SET @ErrorDescription = dbo.CardlessErrorText(@ErrorDescription)
      
      -- Build Error Message
      SET @EventLogDesc = 'Description:'      + @ErrorDescription +
                          ', Card Account:'   + @CardAccount +
                          ', Machine Number:' + @MachineNumber + @ErrorText
      
      -- Test if the Machine should be loocked up.
      IF (@LockupMachine <> 0)
         BEGIN
            UPDATE MACH_SETUP SET ACTIVE_FLAG = 0 WHERE MACH_NO = @MachineNumber AND ACTIVE_FLAG = 1
            
            SET @EventCode = 'SD'
         END
      
      -- Insert Error into the Casino_Event_Log
      INSERT INTO CASINO_EVENT_LOG
         (EVENT_CODE, EVENT_SOURCE, EVENT_DESC, ERROR_NO, MACH_NO)
      VALUES
         (@EventCode, @ErrorSource, @EventLogDesc, @ErrorID, @MachineNumber)  
      
      SET @Balance = 0
   END

-- Log transaction.
EXEC @CasinoTransID = tpInsertCasinoTrans
   @CasinoID, @DealNo, @RollNo, @TicketNo, 0, @TransAmt, @Barcode,
   @TransID, @AcctDate, @TimeStamp, @MachineNumber, @CardAccount,
   @Balance, @MachGameCode, @CoinsBet, @LinesBet, @TierLevel,
   0, @LocationID, @MachineTimeStamp

-- If debugging, save return values...
IF (@Debug = 1)
   BEGIN
      SET @MsgText =
         'tpTransE Return Values - ErrorID: ' + CAST(@ErrorID AS VarChar(10)) +
         '  ErrorDescription: ' + @ErrorDescription +
         '  LockupMachine: ' + CAST(@LockupMachine AS VarChar(10))
      
      EXEC InsertDebugInfo @TransID, @MsgText, @MachNbrAsInt
   END

-- Return results to client
SELECT
   @ErrorID          AS ErrorID,
   @ErrorDescription AS ErrorDescription,
   @LockupMachine    AS ShutDownFlag
GO
