SET QUOTED_IDENTIFIER ON
GO
SET ANSI_NULLS OFF
GO
/*
--------------------------------------------------------------------------------
User Stored Procedure tpTransECR

Created 04-20-2005 by Terry Watkins

Desc: Handles promotional eCoupon Redemption

Return values: ErrorCode, ErrorDescription, ShutdownFlag

Called by: Transaction Portal

Parameters:
   @MachineNumber       The Machine number played
   @CardAccount         Card Account Number in Machine
   @MachineSequence     Transaction sequence number generated by the TransPortal
   @TransAmt            Value in cents of the redeemed promotional coupon


Change Log:

Changed By    Date           Database Version
  Change Description
--------------------------------------------------------------------------------
Terry Watkins 04-20-2005     v4.1.1
  Initial coding

Terry Watkins 06-15-2005     v4.1.4
  Changed size of @CardAccount from 16 to 20.

Terry Watkins 09-07-2005     v4.2.4
  Changed the value inserted into CASINO_TRANS.BALANCE to be the total of the
  account balance plus the promo balance.

Terry Watkins 01-16-2006     v5.0.1
  Modified update to MACH_SETUP.ACTIVE_FLAG so that on error, the flag is set
  to 0 only if it is currently set to 1 (data type changed from Bit to TinyInt).

Terry Watkins 10-04-2006     v5.0.8
  Removed @TicketStatusID from call to tpInsertCasinoTrans. No longer needed
  because column CASINO_TRANS.TICKET_STATUS_ID has been removed.

Terry Watkins 11-03-2009     v7.0.0
  Uses new function dbo.ufnGetAcctDate() to retrieve the accounting date.
  @AcctDate changed from VarChar(16) to DateTime.

Terry Watkins 05-12-2010     v7.2.1
  Modified insert into CASINO_EVENT_LOG to update new columns ERROR_NO and
  MACH_NO.

Terry Watkins 11-11-2010     DCLottery v1.0.0
  Added retrieval of @LocationID to support addition of column
  CASINO_TRANS.LOCATION_ID
  
Louis Epstein 10-25-2013     v3.1.6
  Added machine timestamp functionality
  
Louis Epstein 06-18-2014     v3.2.3
  Modified stat recording functionality to use tpUpdatePlayStatsHourlyAndDaily
--------------------------------------------------------------------------------
*/
CREATE Procedure [dbo].[tpTransECR]
   @MachineNumber       Char(5),
   @CardAccount         VarChar(20),
   @MachineSequence     Int,
   @TransAmt            Int,
   @TimeStamp DateTime
AS

-- Variable Declarations
DECLARE @AcctDate           DateTime
DECLARE @Balance            Money
DECLARE @CardRequired       Bit
DECLARE @CasinoID           VarChar(6)
DECLARE @CasinoMachNo       VarChar(8)
DECLARE @CTBalance          Money
DECLARE @DealNo             Int
DECLARE @Debug              Bit
DECLARE @ErrorDescription   VarChar(256)
DECLARE @ErrorID            Int
DECLARE @ErrorSource        VarChar(64)
DECLARE @EventCode          VarChar(2)
DECLARE @EventLogDesc       VarChar(1024)
DECLARE @LocationID         Int
DECLARE @LockupMachine      Int
DECLARE @MachGameCode       VarChar(3)
DECLARE @MachNbrAsInt       Int
DECLARE @MsgText            VarChar(1024)
DECLARE @PKID               Int
DECLARE @PromoBalance       Money
DECLARE @MachineTimeStamp   DateTime
DECLARE @TransAmount        Money
DECLARE @TransID            SmallInt


-- Variable Initialization
SET @MachineTimeStamp = @TimeStamp
SET @CardRequired     = 1
SET @DealNo           = 0
SET @ErrorDescription = ''
SET @ErrorID          = 0
SET @ErrorSource      = 'tpTransECR Stored Procedure'
SET @EventCode        = 'SP'
SET @EventLogDesc     = ''
SET @LocationID       = 0
SET @LockupMachine    = 0
SET @MachNbrAsInt     = CONVERT(Int, @MachineNumber)
SET @TimeStamp        = GetDate()
SET @TransAmount      = CONVERT(Money, @TransAmt) / 100
SET @TransID          = 19

-- Check Debug mode...
SELECT @Debug = ISNULL(DEBUG_MODE, 0) FROM DEBUG_SETTING WHERE DEBUG_ENTITY = 'tpTransECR'

-- If Debug mode, save argument values...
IF (@Debug = 1)
   -- Record argument values.
   BEGIN
      SET @MsgText =
         'tpTransECR Argument Values:  ' + 
         '  MachineNumber: ' + @MachineNumber +
         '  CardAccount: ' + @CardAccount +
         '  TransAmt: ' + CAST(@TransAmt AS VarChar) +
         '  MachineSequence: ' + CAST(@MachineSequence AS VarChar)
      
      EXEC InsertDebugInfo @TransID, @MsgText, @MachNbrAsInt
   END

-- Retrieve CASINO information.
SELECT
   @CasinoID     = CAS_ID,
   @CardRequired = PLAYER_CARD,
   @LocationID   = LOCATION_ID
FROM CASINO
WHERE SETASDEFAULT = 1

-- If @CardRequired is 0 (TITO), set @CardAccount to 'INTERNAL'
IF (@CardRequired = 0)
   SET @CardAccount = 'INTERNAL'

-- Get the current Accounting Date.
SET @AcctDate = dbo.ufnGetAcctDate()

-- Test for Invalid, Inactive, or Removed Machine.
SET @ErrorID = dbo.GetMachineStatus(@MachineNumber)


-- Set last activity & retrieve Machine info if the machine exists in MACH_SETUP.
IF (@ErrorID <> 104)
   UPDATE MACH_SETUP SET
      LAST_ACTIVITY = @TimeStamp,
      @CasinoMachNo = CASINO_MACH_NO,
      @MachGameCode = GAME_CODE,
      @Balance      = BALANCE,
      @PromoBalance = ISNULL(PROMO_BALANCE, 0)
   WHERE MACH_NO = @MachineNumber

-- Handle any errors
IF (@ErrorID <> 0)
   BEGIN
      SELECT
         @ErrorDescription = ISNULL([DESCRIPTION], 'Undefined Error ' + CAST(@ErrorID AS VarChar(10))),
         @LockupMachine    = ISNULL(LOCKUP_MACHINE, 1)
      FROM ERROR_LOOKUP
      WHERE ERROR_NO = @ErrorID
      
      -- If in Cardless Play mode, reset error text.
      IF (@CardRequired = 0)
         SET @ErrorDescription = dbo.CardlessErrorText(@ErrorDescription)
      
      -- Build Error Message
      SET @EventLogDesc = 'Description: ' + @ErrorDescription +
         ', Card Account: ' + @CardAccount +
         ', Machine Number: ' + @MachineNumber
      
      -- Test if the Machine should be locked up.
      IF (@LockupMachine <> 0)
         BEGIN
            UPDATE MACH_SETUP SET ACTIVE_FLAG = 0 WHERE MACH_NO = @MachineNumber AND ACTIVE_FLAG = 1
            
            SET @EventCode = 'SD'
         END
      
      -- Insert Error into the Casino_Event_Log
      INSERT INTO CASINO_EVENT_LOG
         (EVENT_CODE, EVENT_SOURCE, EVENT_DESC, ERROR_NO, MACH_NO)
      VALUES
         (@EventCode, @ErrorSource, @EventLogDesc, @ErrorID, @MachineNumber)  
      
      SET @Balance = 0
   END

-- Are we error free?
IF (@ErrorID = 0)
   -- Yes, so update the MACH_SETUP.PROMO_BALANCE column and then update
   -- the MACHINE_STATS PROMO_IN columns...
   BEGIN
      -- Calc the new Promo balance.
      SET @PromoBalance = @PromoBalance + @TransAmount
      UPDATE MACH_SETUP SET PROMO_BALANCE = @PromoBalance WHERE MACH_NO = @MachineNumber
      
      EXEC tpUpdatePlayStatsHourlyAndDaily
      @LocationIdPass = @LocationID, 
@TimeStampPass = @TimeStamp,
@MachNoPass = @MachineNumber,
@CasinoMachNoPass = @CasinoMachNo,
@AcctDatePass = @AcctDate,
@DealNoPass = @DealNo,
@GameCodePass = @MachGameCode,
@PromoInAmountPass = @TransAmount,
@PromoInCountPass = 1

   END

-- Calculate the balance that will be recorded in CASINO_TRANS.
SET @CTBalance = @Balance + @PromoBalance

-- Log the transaction in CASINO_TRANS.
EXEC tpInsertCasinoTrans
   @CasinoID        = @CasinoID,
   @DealNo          = @DealNo,
   @RollNo          = 0,
   @TicketNo        = 0,
   @Denom           = 0,
   @TransAmt        = @TransAmount,
   @Barcode         = '',
   @TransID         = @TransID,
   @CurrentAcctDate = @AcctDate,
   @TimeStamp       = @TimeStamp,
   @MachineNumber   = @MachineNumber,
   @CardAccount     = @CardAccount,
   @Balance         = @CTBalance,
   @GameCode        = @MachGameCode,
   @CoinsBet        = 0,
   @LinesBet        = 0,
   @TierLevel       = 0,
   @PressUpCount    = 0,
   @LocationID      = @LocationID,
   @MachineTimeStamp = @MachineTimeStamp


-- Return error info as a dataset.
SELECT
   @ErrorID AS ErrorID,
   @ErrorDescription AS ErrorDescription,
   @LockupMachine AS ShutDownFlag
GO
