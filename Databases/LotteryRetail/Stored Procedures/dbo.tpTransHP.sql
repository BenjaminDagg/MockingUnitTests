SET QUOTED_IDENTIFIER ON
GO
SET ANSI_NULLS ON
GO
/*
--------------------------------------------------------------------------------
User Stored Procedure tpTransHP

Created 12-06-2004 by Terry Watkins

Desc: Handles Hand-Pay transactions.  These are payments made at the Game Machine
      to patrons by Casino personnel when there are ticket printing problems.

Return values: ErrorCode, ErrorDescription, ShutdownFlag

Called by: Transaction Portal

Parameters:
   @MachineNumber       The Machine number played
   @CardAccount         Card Account Number in Machine
   @TransAmount         Amount in cents of the Hand Pay
   @MachineBalance      Machine Balance (after machine subtracts TransAmount)
   @MachineSequence     Transaction sequence number generated by the TransPortal
   @TimeStamp           Machine timestamp (overwritten with server time)

Change Log:
Changed By    Date
  Change Description         Database Version
--------------------------------------------------------------------------------
Terry Watkins 11-04-2004     v4.0.0
  Initial Coding.

Terry Watkins 02-01-2005     v4.0.1
  Hard coded Card Account No to 'INTERNAL' for CASINO_TRANS insertion.

Terry Watkins 06-15-2005     v4.1.4
  Changed size of @CardAccount from 16 to 20.

Terry Watkins 09-07-2005     v5.0.0
  Changed the value inserted into CASINO_TRANS.BALANCE to be the total of the
  account balance plus the promo balance.

Terry Watkins 01-16-2006     v5.0.1
  Modified update to MACH_SETUP.ACTIVE_FLAG so that on error, the flag is set
  to 0 only if it is currently set to 1 (data type changed from Bit to TinyInt).

Terry Watkins 10-04-2006     v5.0.8
  Removed @TicketStatusID. No longer needed because column
  CASINO_TRANS.TICKET_STATUS_ID has been removed.

  Removed hard coded 'INTERNAL' entry into CASINO_TRANS and use the actual
  account number passed to this routine.

Terry Watkins 11-03-2009     v7.0.0
  Added code to disallow balance adjustments that exceed the value set in
  CASINO.MAX_BAL_ADJUSTMENT.
  Uses new function dbo.ufnGetAcctDate() to retrieve the accounting date.
  @AcctDate changed from VarChar(16) to DateTime.

Terry Watkins 05-12-2010     v7.2.1
  Modified insert into CASINO_EVENT_LOG to update new columns ERROR_NO and
  MACH_NO.

Terry Watkins 11-11-2010     DCLottery v1.0.0
  Added retrieval of @LocationID to support addition of column
  CASINO_TRANS.LOCATION_ID
  Added insertion into (or updating of) table PLAY_STATS_HOURLY.

Terry Watkins 11-11-2010     LotteryRetail v3.0.4
  Added use of named parameters when calling tpInsertCasinoTrans.  This fixes a
  bug that caused the LocationID to be inserted into the PressUpCount column.
  
Louis Epstein 10-25-2013     v3.1.6
  Added machine timestamp functionality
  
Louis Epstein 06-18-2014     v3.2.3
  Modified stat recording functionality to use tpUpdatePlayStatsHourlyAndDaily
--------------------------------------------------------------------------------
*/
CREATE PROCEDURE [dbo].[tpTransHP]
   @MachineNumber       Char(5),
   @CardAccount         VarChar(20),
   @TransAmount         Int,
   @MachineBalance      Int,
   @MachineSequence     Int,
   @TimeStamp           DateTime
AS

-- Variable Declarations
DECLARE @AcctDate          DateTime
DECLARE @Balance           Money
DECLARE @Barcode           VarChar(128)
DECLARE @CardRequired      Bit
DECLARE @CasinoID          VarChar(6)
DECLARE @CasinoMachNo      VarChar(8)
DECLARE @CasinoTransID     Int
DECLARE @CoinsBet          Int
DECLARE @CTBalance         Money
DECLARE @DealNo            Int
DECLARE @Debug             Bit
DECLARE @Denom             SmallMoney
DECLARE @ErrorDescription  VarChar(256)
DECLARE @ErrorID           Int
DECLARE @ErrorSource       VarChar(64)
DECLARE @ErrorText         VarChar(1024)
DECLARE @EventCode         VarChar(2)
DECLARE @EventLogDesc      VarChar(1024)
DECLARE @HourOfDay         Int
DECLARE @LocationID        Int
DECLARE @LockupMachine     Int
DECLARE @LinesBet          Int
DECLARE @MachGameCode      VarChar(3)
DECLARE @MachineBal        Money
DECLARE @MachNbrAsInt      Int
DECLARE @MaxBalAdjust      Money
DECLARE @MsgText           VarChar(1024)
DECLARE @PlayStatsHourlyID Int
DECLARE @PromoBalance      Money
DECLARE @RollNo            Int
DECLARE @TicketNo          Int
DECLARE @TierLevel         SmallInt
DECLARE @ToTime            DateTime
DECLARE @TransAmt          Money
DECLARE @TransID           SmallInt
DECLARE @MachineTimeStamp  DateTime

-- Variable Initialization
-- Convert balance first, other vars may depend on it...
SET @MachineBal        = CAST(@MachineBalance AS Money) / 100

SET @MachineTimeStamp  = @TimeStamp
SET @Balance           = 0
SET @Barcode           = ''
SET @CardRequired      = 1
SET @CasinoMachNo      = @MachineNumber
SET @CoinsBet          = 0
SET @CTBalance         = 0
SET @DealNo            = 0
Set @Denom             = 0
SET @ErrorDescription  = ''
SET @ErrorID           = 0
SET @ErrorSource       = 'tpTransHP Stored Procedure'
SET @ErrorText         = ''
SET @EventCode         = 'SP'
SET @EventLogDesc      = ''
SET @LocationID        = 0
SET @LockupMachine     = 0
SET @LinesBet          = 0
SET @MachNbrAsInt      = CAST(@MachineNumber AS Int)
SET @MachGameCode      = 'UNK'
SET @PromoBalance      = 0
SET @RollNo            = 0
SET @TicketNo          = 0
SET @TierLevel         = 0
SET @TransID           = 27
SET @TimeStamp         = GetDate()
SET @TransAmt          = CAST(@TransAmount AS MONEY) / 100


-- Suppress messages.
SET NOCOUNT ON

-- Check Debug mode...
SELECT @Debug = ISNULL(DEBUG_MODE, 0) FROM DEBUG_SETTING WHERE DEBUG_ENTITY = 'tpTransHP'

-- If Debug mode, save argument values...
IF (@Debug = 1)
   -- Record argument values.
   BEGIN
      SET @MsgText =
         'tpTransHP Argument Values:  ' + 
         '  MachineNumber: '     + @MachineNumber +
         '  CardAccount: '       + @CardAccount +
         '  TransactionAmount: ' + CAST(@TransAmount AS VarChar) +
         '  MachineBalance: '    + CAST(@MachineBalance AS VarChar) +
         '  MachineSequence: '   + CAST(@MachineSequence AS VarChar)
      
      EXEC InsertDebugInfo @TransID, @MsgText, @MachNbrAsInt
   END

-- Retrieve Casino information.
SELECT
   @CasinoID     = CAS_ID,
   @ToTime       = TO_TIME,
   @CardRequired = PLAYER_CARD,
   @MaxBalAdjust = MAX_BAL_ADJUSTMENT,
   @LocationID   = LOCATION_ID
FROM CASINO
WHERE SETASDEFAULT = 1

-- Get Current Accounting Date and hour of the day.
SET @AcctDate  = dbo.ufnGetAcctDate()
SET @HourOfDay = dbo.ufnCurrentHour()

-- Test for Invalid, Inactive, or Removed Machine.
SET @ErrorID = dbo.GetMachineStatus(@MachineNumber)

-- Set last activity & retrieve Machine info if the machine exists in MACH_SETUP.
IF (@ErrorID <> 104)
   UPDATE MACH_SETUP SET
      LAST_ACTIVITY = @TimeStamp,
      @MachGameCode = GAME_CODE,
      @Balance      = BALANCE,
      @PromoBalance = PROMO_BALANCE,
      @CasinoMachNo = CASINO_MACH_NO
   WHERE MACH_NO = @MachineNumber

-- Any errors yet?
IF (@ErrorID = 0)
   BEGIN
      -- Calc the value of the new database balance.
      SET @Balance = @Balance - @TransAmt
      
      -- Check that machine balance agress with the new database balance.
      IF (@MachineBal = @Balance)
         -- Machine balance agrees with MACH_SETUP.BALANCE.
         BEGIN
            -- Check for a negative balance.
            IF (@Balance < 0)
               -- Balance is negative.
               BEGIN
                  SET @ErrorID = 118
                  SET @ErrorText = ', Machine balance is negative: ' + CAST(@Balance AS VarChar(16))
               END
            ELSE
               BEGIN
                  -- Balance is not negative, so update the MACH_SETUP.BALANCE value.
                  UPDATE MACH_SETUP SET BALANCE = @Balance WHERE MACH_NO = @MachineNumber
               END
         END
      ELSE
         -- Balance is off.
         BEGIN
            -- If in Cardless Play mode set error to 105, otherwise to 120.
            -- 120 locks machine, 105 does not.
            IF (@CardRequired = 0)
               SET @ErrorID = 105
            ELSE
               SET @ErrorID = 120
            -- In either case, error text is the same...
            SET @ErrorText = ', Server balance: ' + CAST(@Balance AS VarChar(16)) +
                             ', Machine submitted: ' + CAST(@MachineBal AS VarChar(16))
         END
   END

-- Handle any errors
IF (@ErrorID <> 0)
   BEGIN
      SELECT
         @ErrorDescription = ISNULL([DESCRIPTION], 'Undefined Error ' + CAST(@ErrorID AS VarChar(10))),
         @LockupMachine    = ISNULL(LOCKUP_MACHINE, 1)
      FROM ERROR_LOOKUP
      WHERE ERROR_NO = @ErrorID
      
      -- If in Cardless Play mode, reset error text.
      IF (@CardRequired = 0)
         SET @ErrorDescription = dbo.CardlessErrorText(@ErrorDescription)
      
      -- Build Error Message
      SET @EventLogDesc = 'Description: '      + @ErrorDescription +
                          ', Card Account: '   + @CardAccount +
                          ', Machine Number: ' + @MachineNumber + @ErrorText
      
      -- Test if the Machine should be locked up.
      IF (@LockupMachine <> 0)
         BEGIN
            UPDATE MACH_SETUP SET ACTIVE_FLAG = 0 WHERE MACH_NO = @MachineNumber AND ACTIVE_FLAG = 1
            
            SET @EventCode = 'SD'
         END
      
      -- Insert Error into the Casino_Event_Log
      INSERT INTO CASINO_EVENT_LOG
         (EVENT_CODE, EVENT_SOURCE, EVENT_DESC, ERROR_NO, MACH_NO)
      VALUES
         (@EventCode, @ErrorSource, @EventLogDesc, @ErrorID, @MachineNumber)  
      
      SET @Balance = 0
   END

-- Calculate the CASINO_TRANS balance as the account plus promo balances.
SET @CTBalance = @Balance + @PromoBalance

-- Log transaction.
EXEC @CasinoTransID = tpInsertCasinoTrans
   @CasinoID        = @CasinoID,
   @DealNo          = @DealNo,
   @RollNo          = @RollNo,
   @TicketNo        = @TicketNo,
   @Denom           = @Denom,
   @TransAmt        = @TransAmt,
   @Barcode         = @Barcode,
   @TransID         = @TransID,
   @CurrentAcctDate = @AcctDate,
   @TimeStamp       = @TimeStamp,
   @MachineNumber   = @MachineNumber,
   @CardAccount     = @CardAccount,
   @Balance         = @CTBalance,
   @GameCode        = @MachGameCode,
   @CoinsBet        = @CoinsBet,
   @LinesBet        = @LinesBet,
   @TierLevel       = @TierLevel,
   @PressUpCount    = 0,
   @LocationID      = @LocationID,
   @MachineTimeStamp = @MachineTimeStamp

-- Did we get a balance error in Cardless Play mode where the machine
-- balance vs system balance <= max allowable balance adjustment amount?
IF (@ErrorID = 105)
   BEGIN
      IF (@MachineBal - @Balance <= @MaxBalAdjust)
         BEGIN
            -- Yes, so we need to adjust the machine balance...
            EXEC tpAdjustMachineBalance
               @MachineNumber, @CardRequired, @MachineBal, @Balance, 'tpTransHP',
               @MachineTimeStamp, @CasinoID, @AcctDate, @MachGameCode, @LocationID
         END
      ELSE
         BEGIN
            -- No, the adjustment amount exceeds the ajustment threshold, so
            -- force a lockup regardless of the ERROR_LOOKUP setting for 105.
            SET @LockupMachine = 1
         END
   END

-- Update MACHINE_STATS as if a ticket were printed
IF (@CardRequired = 0)
   BEGIN
      IF (@ErrorID = 0 OR (@ErrorID = 105 AND @LockupMachine = 0))
         BEGIN
            
  DECLARE @MachineDenomCast smallmoney
  SET @MachineDenomCast = CAST(@Denom AS SmallMoney)
      
  EXEC tpUpdatePlayStatsHourlyAndDaily 
  @LocationIdPass = @LocationID,
@TimeStampPass = @TimeStamp,
@DenomPass = @MachineDenomCast,
@MachNoPass = @MachineNumber,
@CasinoMachNoPass = @CasinoMachNo,
@AcctDatePass = @AcctDate,
@DealNoPass = @DealNo,
@GameCodePass = @MachGameCode,
@TicketOutCountPass = 1,
@TicketOutAmountPass = @TransAmt
            
         END
   END


-- If debugging, save return values...
IF (@Debug = 1)
   BEGIN
      SET @MsgText =
         'tpTransHP Return Values - ErrorID: ' + CAST(@ErrorID AS VarChar(10)) +
         '  ErrorDescription: ' + @ErrorDescription +
         '  LockupMachine: ' + CAST(@LockupMachine AS VarChar(10))
      
      EXEC InsertDebugInfo @TransID, @MsgText, @MachNbrAsInt
   END

-- Return results to client.
SELECT
   @ErrorID          AS ErrorID,
   @ErrorDescription AS ErrorDescription,
   @LockupMachine    AS ShutDownFlag
GO
