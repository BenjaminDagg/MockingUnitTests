SET QUOTED_IDENTIFIER ON
GO
SET ANSI_NULLS ON
GO
/*
--------------------------------------------------------------------------------
User Stored Procedure tpTransM

Created 04-11-2004 by Chris Coddington

Desc: Handles Money into Machine transactions.

Return Dataset: ErrorCode, ErrorDescription, ShutdownFlag

Called by: Transaction Portal

Parameters:
   @CardAccount         Card Account Number
   @MachineBalance      Machine Balance in cents
   @MachineDenomination Machine Denomination in cents
   @MachineNumber       Machine Number
   @MachineSequence     Transaction Sequence number generated by the Machine
   @TimeStamp           Date and Time from the machine (not used, gets reset)
   @TransAmt            Transaction Amount in cents


Change Log:

Changed By    Date           Database Version
  Change Description
--------------------------------------------------------------------------------
Chris C.      07-25-2003
  Removed machine denomination validation.

Chris C.      02-10-2004
  Changed float datatypes to money datatypes.

Terry Watkins 02-23-2004
  Changed @TransAmount argument datatype from Float to Int.
  Added code to update new MACHINE_STATS table.
  Added insertion of the Game Code of the Machine into the CASINO_TRANS table.

Terry Watkins 11-02-2004     v4.0.0
  Added update of MACHINE_STATS.BILL_COUNT_n column.
  Added update of MACH_SETUP.BALANCE column.
  Added insertion of TransID into CASINO_TRANS.
  Added support for Cardless Play.

Terry Watkins 01-13-2005     v4.0.1
  Added code to update new MACHINE_METER table.

A. Murthy     01-26-2005     v4.0.1
  For TITO set @CardAccount = 'INTERNAL'.

Terry Watkins 05-22-2005     v4.1.4
  Changed size of @CardAccount from 16 to 20.

Terry Watkins 09-07-2005     v5.0.0
  Changed the value inserted into CASINO_TRANS.BALANCE to be the total of the
  account balance plus the promo balance.

Terry Watkins 01-16-2006     v5.0.1
  Modified update to MACH_SETUP.ACTIVE_FLAG so that on error, the flag is set
  to 0 only if it is currently set to 1 (data type changed from Bit to TinyInt).

Terry Watkins 10-04-2006     v5.0.8
  Removed @TicketStatusID. No longer needed because column
  CASINO_TRANS.TICKET_STATUS_ID has been removed.

Terry Watkins 11-03-2009     v7.0.0
  Added code to disallow balance adjustments that exceed the value set in
  CASINO.MAX_BAL_ADJUSTMENT.
  Uses new function dbo.ufnGetAcctDate() to retrieve the accounting date.
  @AcctDate changed from VarChar(16) to DateTime.

Terry Watkins 05-12-2010     v7.2.1
  Modified insert into CASINO_EVENT_LOG to update new columns ERROR_NO and
  MACH_NO.

Terry Watkins 11-11-2010     DCLottery v7.2.4
  Added retrieval of @LocationID to support addition of column
  CASINO_TRANS.LOCATION_ID
  Added insertion into (or updating of) table PLAY_STATS_HOURLY.
  Added Update of MACHINE_STATS, MACHINE_METER, and PLAY_STATS_HOURLY when
  @ErrorID = 105.
  
Louis Epstein 10-25-2013     v3.1.6
  Added machine timestamp functionality
  
Louis Epstein 10-28-2013     v3.1.6
  M transaction will be recorded even when a balance transaction takes place
  
Louis Epstein 06-18-2014     v3.2.3
  Modified stat recording functionality to use tpUpdatePlayStatsHourlyAndDaily
--------------------------------------------------------------------------------
*/
CREATE PROCEDURE [dbo].[tpTransM]
   @CardAccount             VarChar(20),
   @MachineBalance          Int,
   @MachineDenomination     Int,
   @MachineNumber           VarChar(5),
   @MachineSequence         Int,
   @TimeStamp               DateTime,
   @TransAmt                Int
AS

-- Variable Declarations
DECLARE @AcctDate           DateTime
DECLARE @Balance            Money
DECLARE @Barcode            VarChar(20)
DECLARE @CardRequired       Bit
DECLARE @CasinoID           VarChar(6)
DECLARE @CasinoMachNo       VarChar(8)
DECLARE @CasinoTransID      Int
DECLARE @CTBalance          Money
DECLARE @CoinsBet           Int
DECLARE @DealNo             Int
DECLARE @Debug              Bit
DECLARE @ErrorID            Int
DECLARE @ErrorSource        VarChar(64)
DECLARE @ErrorText          VarChar(1024)
DECLARE @ErrorDescription   VarChar(256)
DECLARE @EventCode          VarChar(2)
DECLARE @EventLogDesc       VarChar(1024)
DECLARE @HourOfDay          Int
DECLARE @LinesBet           Int
DECLARE @LocationID         Int
DECLARE @LockupMachine      Int
DECLARE @MachGameCode       VarChar(3)
DECLARE @MachineMeterID     Int
DECLARE @MachineStatsID     Int
DECLARE @MachineBal         Money
DECLARE @MachineDenom       Money
DECLARE @MachNbrAsInt       Int
DECLARE @MachNo             VarChar(5)
DECLARE @MaxBalAdjust       Money
DECLARE @MsgText            VarChar(1024)
DECLARE @PlayStatsHourlyID  Int
DECLARE @PromoBalance       Money
DECLARE @RollNo             Int
DECLARE @TicketNo           Int
DECLARE @TierLevel          SmallInt
DECLARE @TransAmount        Money
DECLARE @TransID            SmallInt
DECLARE @MachineTimeStamp   DateTime

SET NOCOUNT ON

-- Variable Initialization
SET @MachineTimeStamp = @TimeStamp
SET @Balance          = 0
SET @Barcode          = ''
SET @CardRequired     = 1
SET @CoinsBet         = 0
SET @CTBalance        = 0
SET @DealNo           = 0
SET @ErrorID          = 0
SET @ErrorDescription = ''
SET @ErrorSource      = 'tpTransM Stored Procedure'
SET @ErrorText        = ''
SET @EventCode        = 'SP'
SET @EventLogDesc     = ''
SET @LinesBet         = 0
SET @LocationID       = 0
SET @LockupMachine    = 0
SET @MachineBal       = CONVERT(Money, @MachineBalance) / 100
SET @MachineDenom     = CONVERT(Money, @MachineDenomination) / 100
SET @MachNbrAsInt     = CONVERT(Int, @MachineNumber)
SET @PromoBalance     = 0
SET @RollNo           = 0
SET @TicketNo         = 0
SET @TierLevel        = 0
SET @TransAmount      = CONVERT(Money, @TransAmt) / 100
SET @TransID          = 20
SET @TimeStamp        = GetDate()


-- Check Debug mode...
SELECT @Debug = ISNULL(DEBUG_MODE, 0) FROM DEBUG_SETTING WHERE DEBUG_ENTITY = 'tpTransM'

-- If Debug mode, save argument values...
IF (@Debug = 1)
   -- Record argument values.
   BEGIN
      SET @MsgText =
         'tpTransM Argument Values:  ' + 
         '  CardAccount: ' + @CardAccount +
         '  MachineBalance: ' + CAST(@MachineBalance AS VarChar) +
         '  MachineDenomination: ' + CAST(@MachineDenomination AS VarChar) +
         '  MachineNumber: ' + @MachineNumber +
         '  MachineSequence: ' + CAST(@MachineSequence AS VarChar) +
         '  TransAmt: ' + CAST(@TransAmt AS VarChar)
      
      EXEC InsertDebugInfo @TransID, @MsgText, @MachNbrAsInt
   END

-- Retrieve CASINO information.
SELECT
   @CasinoID      = CAS_ID,
   @CardRequired  = PLAYER_CARD,
   @MaxBalAdjust  = MAX_BAL_ADJUSTMENT,
   @LocationID    = LOCATION_ID
FROM CASINO
WHERE SETASDEFAULT = 1

-- If @CardRequired is 0 (TITO), set @CardAccount to 'INTERNAL'
IF (@CardRequired = 0)
   SET @CardAccount = 'INTERNAL'

-- Get Current Accounting Date and hour of the day...
SET @AcctDate  = dbo.ufnGetAcctDate()
SET @HourOfDay = dbo.ufnCurrentHour()

-- Test for Invalid, Inactive, or Removed Machine.
SET @ErrorID = dbo.GetMachineStatus(@MachineNumber)
--PRINT 'Return from GetMachineStatus: ' + CAST(@ErrorID AS VarChar)

-- Set last activity & retrieve Machine info if the machine exists in MACH_SETUP.
IF (@ErrorID <> 104)
   UPDATE MACH_SETUP SET
      LAST_ACTIVITY = @TimeStamp,
      @CasinoMachNo = CASINO_MACH_NO,
      @MachGameCode = GAME_CODE,
      @Balance      = BALANCE,
      @PromoBalance = PROMO_BALANCE
   WHERE MACH_NO = @MachineNumber

-- Are we error free and requiring Cards to play?
IF (@ErrorID = 0 AND @CardRequired = 1)
   -- Yes, so retrieve the Machine Number and Balance from the Card Account table.
   BEGIN
      SELECT
        @MachNo = MACH_NO,
        @Balance = BALANCE
      FROM CARD_ACCT
      WHERE CARD_ACCT_NO = @CardAccount

      -- Did we find the CARD_ACCT record?      
      IF (@@ROWCOUNT = 0)
         -- No so set error to Invalid Card.
         SET @ErrorID = 102
         -- Test that the card is in the machine.
      ELSE IF (@MachNo = @MachineNumber)
         BEGIN
            -- Make balance change.
            UPDATE CARD_ACCT SET
               BALANCE = BALANCE + @TransAmount,
               @Balance = BALANCE + @TransAmount
            WHERE CARD_ACCT_NO = @CardAccount
            
            IF (@Balance <> @MachineBal)
               -- Balance is off.
               BEGIN
                  -- In Card Required play mode, set error to 120 (lockup flag = 1)
                  SET @ErrorID = 120
                  SET @ErrorText = ', Machine balance should be: ' +
                     CAST(@Balance AS VarChar(16)) +
                     ', balance submitted was: ' +
                     CAST(@MachineBal AS VarChar(16))
               END
            ELSE IF (@Balance < 0)
               -- Balance is negative.
               BEGIN
                  SET @ErrorID = 118
                  SET @ErrorText = ', Machine balance is negative: ' + CAST(@Balance AS VarChar(16))
               END
         END
   END

-- Are we error free and in Cardless play mode?
IF (@ErrorID = 0 AND @CardRequired = 0)
   -- Yes, so store the updated balance in the MACH_SETUP table.
   BEGIN
      -- We already have the Balance from the MACH_SETUP table
      UPDATE MACH_SETUP SET
         BALANCE = BALANCE + @TransAmount,
         @Balance = BALANCE + @TransAmount
      WHERE MACH_NO = @MachineNumber
      
      IF (@Balance <> @MachineBal)
         -- Balance is off.
         BEGIN
            -- In Cardless Play mode, set error to 105 (lockup flag = 0)
            SET @ErrorID = 105
            SET @ErrorText = ', Machine balance should be: ' +
               CAST(@Balance AS VarChar(16)) +
               ', balance submitted was: ' +
               CAST(@MachineBal AS VarChar(16))
         END
      ELSE IF (@Balance < 0)
         -- Balance is negative.
         BEGIN
            SET @ErrorID = 118
            SET @ErrorText = ', Machine balance is negative: ' + CAST(@Balance AS VarChar(16))
         END
      
      -- If the Card account is an empty string, set it to INTERNAL to avoid a Foreign Key constraint violation.
      IF @CardAccount = ''
         SET @CardAccount = 'INTERNAL'
   END

-- Handle any errors
IF (@ErrorID <> 0)
   BEGIN
      SELECT
         @ErrorDescription = ISNULL([DESCRIPTION], 'Undefined Error ' + CAST(@ErrorID AS VarChar(10))),
         @LockupMachine    = ISNULL(LOCKUP_MACHINE, 1)
      FROM ERROR_LOOKUP
      WHERE ERROR_NO = @ErrorID
      
      -- If in Cardless Play mode, reset error text.
      IF (@CardRequired = 0)
         SET @ErrorDescription = dbo.CardlessErrorText(@ErrorDescription)
      
      -- Build Error Message
      SET @EventLogDesc = 'Description: ' + @ErrorDescription +
                          ', Card Account: ' + @CardAccount +
                          ', Machine Number: ' + @MachineNumber + @ErrorText
      
      -- Test if the Machine should be locked up.
      IF (@LockupMachine <> 0)
         BEGIN
            UPDATE MACH_SETUP SET ACTIVE_FLAG = 0 WHERE MACH_NO = @MachineNumber AND ACTIVE_FLAG = 1
            
            SET @EventCode = 'SD'
         END
      
      -- Insert Error into the Casino_Event_Log
      INSERT INTO CASINO_EVENT_LOG
         (EVENT_CODE, EVENT_SOURCE, EVENT_DESC, ERROR_NO, MACH_NO)
      VALUES
         (@EventCode, @ErrorSource, @EventLogDesc, @ErrorID, @MachineNumber)  
      
      SET @Balance = 0
   END

-- Are we error free?
IF (@ErrorID = 0 OR @ErrorID = 105)
   -- Yes, so update MACHINE_STATS table...
   BEGIN
      
      -- Update the appropriate Bill Count column.
      IF (@TransAmt = 100)
         EXEC tpUpdatePlayStatsHourlyAndDaily 
         @LocationIdPass = @LocationID,
@TimeStampPass = @TimeStamp,
@MachNoPass = @MachineNumber,
@CasinoMachNoPass = @CasinoMachNo,
@AcctDatePass = @AcctDate,
@DealNoPass = @DealNo,
@GameCodePass = @MachGameCode,
@AmountInPass = @TransAmount,
@BillCount1Pass = 1
      ELSE IF (@TransAmt = 200)
  EXEC tpUpdatePlayStatsHourlyAndDaily 
  @LocationIdPass = @LocationID,
@TimeStampPass = @TimeStamp,
@MachNoPass = @MachineNumber,
@CasinoMachNoPass = @CasinoMachNo,
@AcctDatePass = @AcctDate,
@DealNoPass = @DealNo,
@GameCodePass = @MachGameCode,
@AmountInPass = @TransAmount,
@BillCount2Pass = 1
      ELSE IF (@TransAmt = 500)
  EXEC tpUpdatePlayStatsHourlyAndDaily 
  @LocationIdPass = @LocationID,
@TimeStampPass = @TimeStamp,
@MachNoPass = @MachineNumber,
@CasinoMachNoPass = @CasinoMachNo,
@AcctDatePass = @AcctDate,
@DealNoPass = @DealNo,
@GameCodePass = @MachGameCode,
@AmountInPass = @TransAmount,
@BillCount5Pass = 1
      ELSE IF (@TransAmt = 1000)
  EXEC tpUpdatePlayStatsHourlyAndDaily 
  @LocationIdPass = @LocationID,
@TimeStampPass = @TimeStamp,
@MachNoPass = @MachineNumber,
@CasinoMachNoPass = @CasinoMachNo,
@AcctDatePass = @AcctDate,
@DealNoPass = @DealNo,
@GameCodePass = @MachGameCode,
@AmountInPass = @TransAmount,
@BillCount10Pass = 1
      ELSE IF (@TransAmt = 2000)
  EXEC tpUpdatePlayStatsHourlyAndDaily 
  @LocationIdPass = @LocationID,
@TimeStampPass = @TimeStamp,
@MachNoPass = @MachineNumber,
@CasinoMachNoPass = @CasinoMachNo,
@AcctDatePass = @AcctDate,
@DealNoPass = @DealNo,
@GameCodePass = @MachGameCode,
@AmountInPass = @TransAmount,
@BillCount20Pass = 1
      ELSE IF (@TransAmt = 5000)
  EXEC tpUpdatePlayStatsHourlyAndDaily 
  @LocationIdPass = @LocationID,
@TimeStampPass = @TimeStamp,
@MachNoPass = @MachineNumber,
@CasinoMachNoPass = @CasinoMachNo,
@AcctDatePass = @AcctDate,
@DealNoPass = @DealNo,
@GameCodePass = @MachGameCode,
@AmountInPass = @TransAmount,
@BillCount50Pass = 1
      ELSE IF (@TransAmt = 10000)
  EXEC tpUpdatePlayStatsHourlyAndDaily 
  @LocationIdPass = @LocationID,
@TimeStampPass = @TimeStamp,
@MachNoPass = @MachineNumber,
@CasinoMachNoPass = @CasinoMachNo,
@AcctDatePass = @AcctDate,
@DealNoPass = @DealNo,
@GameCodePass = @MachGameCode,
@AmountInPass = @TransAmount,
@BillCount100Pass = 1
      ELSE --IF @TransAmt NOT IN (100, 200, 500, 1000, 2000, 5000, 10000)
  EXEC tpUpdatePlayStatsHourlyAndDaily 
  @LocationIdPass = @LocationID,
@TimeStampPass = @TimeStamp,
@MachNoPass = @MachineNumber,
@CasinoMachNoPass = @CasinoMachNo,
@AcctDatePass = @AcctDate,
@DealNoPass = @DealNo,
@GameCodePass = @MachGameCode,
@AmountInPass = @TransAmount,
@BillCountOtherPass = 1
      
      -- Update the MACHINE_METER table.
      UPDATE MACHINE_METER SET
         AMOUNT_IN_TOTAL = AMOUNT_IN_TOTAL + @TransAmount,
         @MachineMeterID = MACHINE_METER_ID
      WHERE
         MACH_NO = @MachineNumber
      
      -- If no row was modified, the record doesn't exist - so create it.
      IF (@@ROWCOUNT = 0)
         BEGIN
            INSERT INTO MACHINE_METER
               (MACH_NO, AMOUNT_IN_TOTAL)
            VALUES
               (@MachineNumber, @TransAmount)
            
            SET @MachineMeterID = @@IDENTITY
         END
      
      -- Update the appropriate amount bucket column.
      IF (@TransAmt = 100)
         UPDATE MACHINE_METER SET AMOUNT_IN_1 = AMOUNT_IN_1 + @TransAmount WHERE MACHINE_METER_ID = @MachineMeterID
      ELSE IF (@TransAmt = 200)
         UPDATE MACHINE_METER SET AMOUNT_IN_2 = AMOUNT_IN_2 + @TransAmount WHERE MACHINE_METER_ID = @MachineMeterID
      ELSE IF (@TransAmt = 500)
         UPDATE MACHINE_METER SET AMOUNT_IN_5 = AMOUNT_IN_5 + @TransAmount WHERE MACHINE_METER_ID = @MachineMeterID
      ELSE IF (@TransAmt = 1000)
         UPDATE MACHINE_METER SET AMOUNT_IN_10 = AMOUNT_IN_10 + @TransAmount WHERE MACHINE_METER_ID = @MachineMeterID
      ELSE IF (@TransAmt = 2000)
         UPDATE MACHINE_METER SET AMOUNT_IN_20 = AMOUNT_IN_20 + @TransAmount WHERE MACHINE_METER_ID = @MachineMeterID
      ELSE IF (@TransAmt = 5000)
         UPDATE MACHINE_METER SET AMOUNT_IN_50 = AMOUNT_IN_50 + @TransAmount WHERE MACHINE_METER_ID = @MachineMeterID
      ELSE IF (@TransAmt = 10000)
         UPDATE MACHINE_METER SET AMOUNT_IN_100 = AMOUNT_IN_100 + @TransAmount WHERE MACHINE_METER_ID = @MachineMeterID
      ELSE --IF @TransAmt NOT IN (100, 200, 500, 1000, 2000, 5000, 10000)
         UPDATE MACHINE_METER SET AMOUNT_IN_OTHER = AMOUNT_IN_OTHER + @TransAmount WHERE MACHINE_METER_ID = @MachineMeterID
      
   END

-- Log transaction.
SET @CTBalance = @Balance + @PromoBalance

EXEC @CasinoTransID = tpInsertCasinoTrans
   @CasinoID, @DealNo, @RollNo, @TicketNo, 0, @TransAmount, @Barcode,
   @TransID, @AcctDate, @TimeStamp, @MachineNumber, @CardAccount,
   @CTBalance, @MachGameCode, @CoinsBet, @LinesBet, @TierLevel,
   0, @LocationID, @MachineTimeStamp


-- Did we get a balance error in Cardless Play mode?
IF (@ErrorID = 105)
   BEGIN
      -- Is the difference <= adjustment threshold?
      IF ((@MachineBal - @Balance) <= @MaxBalAdjust)
         BEGIN
            -- Yes, so we need to adjust the machine balance...
            EXEC tpAdjustMachineBalance
               @MachineNumber, @CardRequired, @MachineBal, @Balance, 'tpTransM',
               @MachineTimeStamp, @CasinoID, @AcctDate, @MachGameCode, @LocationID
         END
      ELSE
         -- The difference exceeds the max adjustment value, force a lockup.
         BEGIN
            SET @LockupMachine = 1
         END
   END

-- If debugging, save return values...
IF (@Debug = 1)
   BEGIN
      SET @MsgText =
         'tpTransM Return Values - ErrorID: ' + CAST(@ErrorID AS VarChar(10)) +
         '  ErrorDescription: ' + @ErrorDescription +
         '  LockupMachine: ' + CAST(@LockupMachine AS VarChar(10))
      
      EXEC InsertDebugInfo @TransID, @MsgText, @MachNbrAsInt
   END

-- Return results to client
SELECT
   @ErrorID AS ErrorID,
   @ErrorDescription AS ErrorDescription,
   @LockupMachine AS ShutDownFlag
GO
