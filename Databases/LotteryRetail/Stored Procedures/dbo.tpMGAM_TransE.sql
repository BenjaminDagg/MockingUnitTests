SET QUOTED_IDENTIFIER OFF
GO
SET ANSI_NULLS OFF
GO
/*
--------------------------------------------------------------------------------
User Stored Procedure tpMGAM_TransE

Created 04-29-2005 by A Murthy

Desc: Handles all MGAM card extract requests.

Return values: ErrorID, ErrorDescription, ShutDownFlag

Called by: Transaction Portal

Parameters: 
   @CardAccount         Card Account Number
   @MachineBalance      Balance sent by the Machine in cents
   @MachineDenomination Machine Denomination in cents
   @MachineNumber       Machine Number (identifier)
   @MachineSequence     Sequence Number generated by the Machine
   @TimeStamp           Date and Time from the Machine (but overwritten here)
   @PromoBalance        Coupon Balance sent by the Machine in cents

Change Log:

Changed By    Date           Database Version
  Change Description
--------------------------------------------------------------------------------
A. Murthy     04/29/2005     v4.0.x
  Initial Coding

A Murthy      11-09-2005     v4.2.4
  Added check for ErrorID=102 to tpInsertCasinoTrans to avoid SQLException when
  a card was extracted that was not present in CARD_ACCT table.

A. Murthy     01-16-2006     v5.0.1
  1) Modified update to MACH_SETUP.ACTIVE_FLAG so that on error, the flag is set
     to 0 only if it is currently set to 1 (data type changed from Bit to TinyInt).
  2) When @Balance <> @MachineBal Force the machine balance into the CARD_ACCT table.

Terry Watkins 10-04-2006     v5.0.8
  Removed @TicketStatusID (CASINO_TRANS.TICKET_STATUS_ID has been removed).

Terry Watkins 11-03-2009     v7.0.0
  Uses new function dbo.ufnGetAcctDate() to retrieve the accounting date.
  @AcctDate changed from VarChar(16) to DateTime.

Terry Watkins 05-12-2010     v7.2.1
  Modified insert into CASINO_EVENT_LOG to update new columns ERROR_NO and
  MACH_NO.

Terry Watkins 11-11-2010     DCLottery v1.0.0
  Added retrieval of @LocationID to support addition of column
  CASINO_TRANS.LOCATION_ID
--------------------------------------------------------------------------------
*/
CREATE Procedure [dbo].[tpMGAM_TransE]
   @CardAccount         VarChar(20),
   @MachineBalance      Int,
   @MachineDenomination Int,
   @MachineNumber       VarChar(5),
   @MachineSequence     Int,
   @TimeStamp           DateTime,
   @PromoBalance        Int
AS

-- Variable Declarations
DECLARE @AcctDate         DateTime
DECLARE @Balance          Money
DECLARE @BalanceChange    Money
DECLARE @BalancePromo     Money
DECLARE @Barcode          VarChar(128)
DECLARE @CardRequired     Bit
DECLARE @CasinoID         VarChar(6)
DECLARE @CasinoMachNo     VarChar(8)
DECLARE @CasinoTransID    Int
DECLARE @CoinsBet         Int
DECLARE @CTBalance        Money
DECLARE @Debug            Bit
DECLARE @DealNo           Int
DECLARE @Denomination     Money
DECLARE @ErrorID          Int
DECLARE @ErrorSource      VarChar(64)
DECLARE @ErrorText        VarChar(1024)
DECLARE @ErrorDescription VarChar(256)
DECLARE @EventCode        VarChar(2)
DECLARE @EventDesc        VarChar(1024)
DECLARE @EventSource      VarChar(64)
DECLARE @EventLogDesc     VarChar(1024)
DECLARE @IsActive         Int
DECLARE @LinesBet         Int
DECLARE @LocationID       Int
DECLARE @LockupMachine    Int
DECLARE @MachineBal       Money
DECLARE @MachineDenom     Money
DECLARE @MachNo           VarChar(5)
DECLARE @MachGameCode     VarChar(3)
DECLARE @MachNbrAsInt     Int
DECLARE @MsgText          VarChar(1024)
DECLARE @PromoBal         Money
DECLARE @RollNo           Int
DECLARE @TicketNo         Int
DECLARE @TierLevel        SmallInt
DECLARE @TransAmt         Money
DECLARE @TransID          SmallInt

SET NOCOUNT ON

-- Variable Initialization
SET @Balance          = 0
SET @BalanceChange    = 0
SET @BalancePromo     = 0
SET @Barcode          = ''
SET @CardRequired     = 1
SET @CoinsBet         = 0
SET @CTBalance        = 0
SET @Denomination     = 0
SET @DealNo           = 0
SET @ErrorID          = 0
SET @ErrorDescription = ''
SET @ErrorSource      = 'tpMGAM_TransE Stored Procedure'
SET @ErrorText        = ''
SET @EventCode        = 'SP'
SET @EventSource      = 'tpMGAM_TransE'
SET @IsActive         = 0
SET @LinesBet         = 0
SET @LocationID       = 0
SET @LockupMachine    = 0
SET @MachineBal       = CONVERT(Money, @MachineBalance) / 100
SET @MachineDenom     = CONVERT(Money, @MachineDenomination) / 100
SET @MachNbrAsInt     = CAST(@MachineNumber AS Int)
SET @PromoBal         = CONVERT(Money, @PromoBalance) / 100
SET @RollNo           = 0
SET @TicketNo         = 0
SET @TierLevel        = 0
SET @TransAmt         = 0
SET @TransID          = 41
SET @TimeStamp        = GetDate()

-- Calc the balance we insert into CASINO_TRANS as Machine + promo balances.
SET @CTBalance = @MachineBal + @PromoBal

-- Check Debug mode...
SELECT @Debug = ISNULL(DEBUG_MODE, 0) FROM DEBUG_SETTING WHERE DEBUG_ENTITY = 'tpMGAM_TransE'

-- Are we in Debug mode?
IF (@Debug = 1)
   -- Yes, so record incoming argument values...
   BEGIN
      SET @MsgText =
         'tpMGAM_TransE Argument Values:  ' + 
         '  CardAccount: ' + @CardAccount +
         '  MachineBalance: ' + CAST(@MachineBalance AS VarChar) +
         '  MachineDenomination: ' + CAST(@MachineDenomination AS VarChar) +
         '  MachineNumber: ' + @MachineNumber +
         '  MachineSequence: ' + CAST(@MachineSequence AS VarChar) +
         '  TimeStamp: ' + CAST(@TimeStamp AS VarChar) +
         '  PromoBalance: ' + CAST(@PromoBalance AS VarChar)
      
      EXEC InsertDebugInfo @TransID, @MsgText, @MachNbrAsInt
   END

-- Retrieve CASINO information.
SELECT
   @CasinoID     = CAS_ID,
   @CardRequired = PLAYER_CARD,
   @LocationID   = LOCATION_ID
FROM CASINO
WHERE SETASDEFAULT = 1

-- Get Current Accounting Date.
SET @AcctDate = dbo.ufnGetAcctDate()

-- Call GetMachineStatus function (checks for Machine not found, inactive, or removed).
SET @ErrorID = dbo.GetMachineStatus(@MachineNumber)

-- If MACH_SETUP record exists, Update MACH_SETUP.LAST_ACTIVITY and retrive GAME_CODE for the machine.
IF (@ErrorID <> 104)
   UPDATE MACH_SETUP SET
      LAST_ACTIVITY = @TimeStamp,
      @CasinoMachNo = CASINO_MACH_NO,
      @MachGameCode = GAME_CODE
   WHERE MACH_NO = @MachineNumber

-- Are we still error free?
IF (@ErrorID = 0)
   BEGIN
      -- Yes, so retrieve machine number balance from CARD_ACCT.
      SELECT @MachNo = MACH_NO, @Balance = BALANCE
      FROM CARD_ACCT
      WHERE CARD_ACCT_NO = @CardAccount
      
      -- Test that the card exists.
      IF (@@ROWCOUNT > 0)
         BEGIN
            -- Test that the card is in the machine.
            IF (@MachNo = @MachineNumber)
               BEGIN
                  -- Remove Machine reference  and Session Date in the Card Account Table
                  UPDATE CARD_ACCT SET
                     MACH_NO      = 0,
                     SESSION_DATE = NULL
                  WHERE CARD_ACCT_NO = @CardAccount 
 
                  IF (@Balance <> @MachineBal)
                     BEGIN
                        -- Calc. the difference between Machine & CARD_ACCT balances.
                        SET @BalanceChange = @MachineBal - @Balance 
                        
                        -- Balance is Off, Force the machine balance into the CARD_ACCT table.
                        UPDATE CARD_ACCT SET BALANCE = @MachineBal WHERE CARD_ACCT_NO = @CardAccount
                        
                        -- Log this balance adjustment in CASINO_TRANS
                        SET @TransAmt = @BalanceChange        
                        EXEC @CasinoTransID = tpInsertCasinoTrans
                             @CasinoID, @DealNo, @RollNo, @TicketNo, 0, @TransAmt, @Barcode,
                             @TransID, @AcctDate, @TimeStamp, @MachineNumber, @CardAccount,
                             @CTBalance, @MachGameCode, @CoinsBet, @LinesBet, @TierLevel, @LocationID
                        
                        -- Build the log event description text.
                        SET @EventDesc = 'Server CARD_ACCT Balance Adjusted ' + CONVERT(VarChar, @BalanceChange) +
                        ' from ' + CONVERT(VarChar, @Balance) + ' to ' + CONVERT(VarChar, @MachineBal) + 
                        ', Machine Nbr: ' + @MachineNumber + ', Casino Machine Nbr: ' + @CasinoMachNo + '.'
                        
                        -- Insert a log record.
                        INSERT INTO CASINO_EVENT_LOG
                           (EVENT_CODE, EVENT_SOURCE, EVENT_DESC, EVENT_DATE_TIME, ID_VALUE)
                        VALUES
                           ('BA', @EventSource, @EventDesc, @TimeStamp, @CasinoTransID)
                     END
                  ELSE
                     -- Update CARD_ACCT zero out the Balance.
                     UPDATE CARD_ACCT SET BALANCE = 0 WHERE CARD_ACCT_NO = @CardAccount
                  
                  -- Now retrieve the PromoBalance from MACH_SETUP
                  SELECT @BalancePromo = PROMO_BALANCE 
                     FROM MACH_SETUP
                  WHERE MACH_NO = @MachineNumber
                  
                  IF (@BalancePromo <> @PromoBal)
                     -- PromoBalance is Off
                     BEGIN
                        -- set Error to 132 (Incorrect Promotional Balance)
                        SET @ErrorID = 132
                        SET @ErrorText = 'Machine Promo balance should be: ' + CONVERT(VARCHAR(16), @BalancePromo) + ' Promo balance submitted was: ' + CONVERT(VARCHAR(16), @PromoBal)
                     END
                  ELSE
                     -- Update MACH_SETUP zero out the PromoBalance.
                     UPDATE MACH_SETUP SET PROMO_BALANCE = 0 WHERE MACH_NO = @MachineNumber

                  -- Set the @Balance = 0 for insert into CASINO_TRANS
                  SET @Balance = 0

               END
            ELSE
               -- Card is not in machine.
               SET @ErrorID = 108
         END
      ELSE
         -- Card was not found in the CARD_ACCT table.
         SET @ErrorID = 102
   END
                

-- Handle any errors
IF (@ErrorID <> 0)
   BEGIN
      SELECT
         @ErrorDescription = ISNULL([DESCRIPTION], 'Undefined Error ' + CAST(@ErrorID AS VarChar(10))),
         @LockupMachine    = ISNULL(LOCKUP_MACHINE, 1)
      FROM ERROR_LOOKUP
      WHERE ERROR_NO = @ErrorID
      
      -- If in Cardless Play mode, reset error text.
      IF (@CardRequired = 0)
         SET @ErrorDescription = dbo.CardlessErrorText(@ErrorDescription)
      
      -- Build Error Message
      SET @EventLogDesc = 'Description:' + @ErrorDescription   +
                          ', Card Account:' + @CardAccount     +
                          ', Machine Number:' + @MachineNumber + @ErrorText
      
      -- Test if the Machine should be loocked up.
      IF (@LockupMachine <> 0)
         BEGIN
            UPDATE MACH_SETUP SET ACTIVE_FLAG = 0 WHERE MACH_NO = @MachineNumber AND ACTIVE_FLAG = 1
            SET @EventCode = 'SD'
         END
      
      -- Insert Error into the Casino_Event_Log
      INSERT INTO CASINO_EVENT_LOG
         (EVENT_CODE, EVENT_SOURCE, EVENT_DESC, ERROR_NO, MACH_NO)
      VALUES
         (@EventCode, @ErrorSource, @EventLogDesc, @ErrorID, @MachineNumber)  
      
      SET @Balance = 0
   END

-- Log transaction into CASINO_TRANS, 
-- only if Error is not 102 otherwise you get a Foreign Key constraint failure with CARD_ACCT table.
IF (@ErrorID <> 102 )
   BEGIN
      SET @TransAmt = 0
      EXEC @CasinoTransID = tpInsertCasinoTrans
      @CasinoID, @DealNo, @RollNo, @TicketNo, 0, @TransAmt, @Barcode,
      @TransID, @AcctDate, @TimeStamp, @MachineNumber, @CardAccount,
      @CTBalance, @MachGameCode, @CoinsBet, @LinesBet, @TierLevel, @LocationID
   END

-- Is Debug mode is on?
IF (@Debug = 1)
   -- Yes, so record outgoing argument values...
   BEGIN
      SET @MsgText =
         'tpMGAM_TransE Return Values:  ' + 
         '  ErrorID: '                        + CAST(@ErrorID AS VarChar) +
         '  ErrorDescription: '               + @ErrorDescription +
         '  ShutDownFlag: '                   + CAST(@LockupMachine AS VarChar) +
         '  Machine Promo Balance: '          + CAST(@BalancePromo AS VarChar) +
         '  Incoming PromoBalance: '          + CAST(@PromoBal AS VarChar)
      
      EXEC InsertDebugInfo @TransID, @MsgText, @MachNbrAsInt
   END

-- Return results to client
SELECT
   @ErrorID          AS ErrorID,
   @ErrorDescription AS ErrorDescription,
   @LockupMachine    AS ShutDownFlag
GO
