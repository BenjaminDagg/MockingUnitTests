SET QUOTED_IDENTIFIER ON
GO
SET ANSI_NULLS ON
GO
/*
--------------------------------------------------------------------------------
User Stored Procedure tpTransD

Created 04-11-2003 by Chris Coddington

Desc: Handles all drop requests.

Return values:
   ErrorID
   ErrorDescription
   LockupMachine

Called by: Transaction Portal

Parameters:
   @CardAccount     Card Account Number
   @DropAmount      Drop Amount in cents
   @DB1             Number of 1 Dollar Bills
   @DB2             Number of 2 Dollar Bills
   @DB5             Number of 5 Dollar Bills
   @DB10            Number of 10 Dollar Bills
   @DB20            Number of 20 Dollar Bills
   @DB50            Number of 50 Dollar Bills
   @DB100           Number of 100 Dollar Bills
   @LastDrop        Last Drop Date and Time
   @LosingTabs      Number of Losing tabs
   @MachineNumber   Machine Number
   @MachineSequence Sequence Number generated by the game machine
   @Payout          Amount Paidout
   @TabsSold        Number of Tabs dispensed
   @TimeStamp       Machine Date and Time
   @WinningTabs     Number of Winning tabs dispensed
   @TicketInAmount  Amount in cents of Vouchers redeemed
   @TicketInCount   Number of Vouchers redeemed
   @PromoInAmount   Amount in cents of Promotional Coupons redeemed
   @PromoInCount    Number of Promotional Coupons redeemed


Change Log:
Changed By    Date           Database Version
  Change Description
--------------------------------------------------------------------------------
Chris C.      02-10-2004
  Changed datatype of @Balance and @TransAmt variables from Float to Money.

Terry Watkins 03-01-2004
  Added retrieval of MACH_SETUP.GAME_CODE for insertion into CASINO_TRANS.

Terry Watkins 12-01-2004     v4.0.0
  Added insertion of TransID into Casino_Trans.
  Added parameters @TicketInAmount and @TicketInCount to track voucher drop info.

Terry Watkins 12-01-2004     v4.1.0
  Added parameters @PromoInAmount and @PromoInCount to track promo drop info.

Terry Watkins 06-15-2005     v4.1.4
  Changed size of @CardAccount from 16 to 20.

A.Murthy 06-16-2006          v5.0.4
  Added echo of Input parms and Output values to DEBUG_INFO table.

Terry Watkins 10-04-2006     v5.0.8
  Removed @TicketStatusID. No longer needed because column
  CASINO_TRANS.TICKET_STATUS_ID has been removed.

Terry Watkins 11-03-2009     v7.0.0
  Uses new function dbo.ufnGetAcctDate() to retrieve the accounting date.
  @AcctDate changed from VarChar(16) to DateTime.

Terry Watkins 11-10-2010     DCLRetail v1.0.0
  Added retrieval of Location ID from the CASINO table for insertion into
  DROP_MACHINE.LOCATION_ID.

Terry Watkins 11-11-2010     DCLottery v1.0.0
  Added retrieval of @LocationID to support addition of column
  CASINO_TRANS.LOCATION_ID

Terry Watkins 11-11-2010     LotteryRetail v3.0.4
  Added use of named parameters when calling tpInsertCasinoTrans.  This fixes a
  bug that caused the LocationID to be inserted into the PressUpCount column.
  
Louis Epstein 10-25-2013     v3.1.6
  Added machine timestamp functionality
--------------------------------------------------------------------------------
*/
CREATE PROCEDURE [dbo].[tpTransD]
   @CardAccount     VarChar(20),
   @DropAmount      Int,
   @DB1             Int,
   @DB2             Int,
   @DB5             Int,
   @DB10            Int,
   @DB20            Int,
   @DB50            Int,
   @DB100           Int,
   @LastDrop        DateTime,
   @LosingTabs      Int,
   @MachineNumber   Char(5),
   @MachineSequence Int,
   @Payout          Int,
   @TabsSold        Int,
   @TimeStamp       DateTime,
   @WinningTabs     Int,
   @TicketInAmount  Int,
   @TicketInCount   Int,
   @PromoInAmount   Int = 0,
   @PromoInCount    Int = 0

AS

-- Variables
DECLARE @AcctDate          DateTime
DECLARE @Balance           Money
DECLARE @Barcode           VarChar(128)
DECLARE @CasinoID          VarChar(6)
DECLARE @CasinoTransID     Int
DECLARE @CoinsBet          Int
DECLARE @DealNo            Int
DECLARE @Debug             Bit
DECLARE @ErrorID           Int
DECLARE @ErrorSource       VarChar(64)
DECLARE @ErrorText         VarChar(1024)
DECLARE @ErrorDescription  VarChar(256)
DECLARE @EventCode         VarChar(2)
DECLARE @GenericErrorText  VarChar(1024)
DECLARE @LinesBet          Int
DECLARE @LocationID        Int
DECLARE @LockupMachine     Int
DECLARE @MachGameCode      VarChar(3)
DECLARE @MachNbrAsInt      Int
DECLARE @MsgText           VarChar(2048)
DECLARE @RollNo            Int
DECLARE @TicketNo          Int
DECLARE @TierLevel         SmallInt
DECLARE @TransAmt          Money
DECLARE @TransID           SmallInt
DECLARE @MachineTimeStamp  DateTime

SET NOCOUNT ON

-- Variable Initialization
SET @MachineTimeStamp = @TimeStamp
SET @Balance          = 0
SET @Barcode          = ''
SET @CoinsBet         = 0
SET @DealNo           = 0
SET @ErrorDescription = ''
SET @ErrorID          = 0
SET @ErrorSource      = 'tpTransD Stored Procedure'
SET @ErrorText        = ''
SET @EventCode        = 'SD'
SET @GenericErrorText = ''
SET @LinesBet         = 0
SET @LocationID       = 0
SET @LockupMachine    = 0
SET @MachNbrAsInt     = CAST(@MachineNumber AS Int)
SET @RollNo           = 0
SET @TicketNo         = 0
SET @TierLevel        = 0
SET @TransAmt         = CAST(@DropAmount AS MONEY) / 100
SET @TransID          = 23
SET @TimeStamp        = GetDate()

-- Check Debug mode...
SELECT @Debug = ISNULL(DEBUG_MODE, 0) FROM DEBUG_SETTING WHERE DEBUG_ENTITY = 'tpTransD'

-- Are we in Debug mode?
IF (@Debug = 1)
   -- Yes, so record incoming argument values...
   BEGIN
      SET @MsgText =
         'tpTransD Argument Values:  ' + 
         '  CardAccount: ' + @CardAccount +
         '  DropAmount: ' + CAST(@DropAmount AS VarChar) +
         '  DB1: ' + CAST(@DB1 AS VarChar) +
         '  DB5: ' + CAST(@DB5 AS VarChar) +
         '  DB10: ' + CAST(@DB10 AS VarChar) +
         '  DB20: ' + CAST(@DB20 AS VarChar) +
         '  DB50: ' + CAST(@DB50 AS VarChar) +
         '  DB100: ' + CAST(@DB100 AS VarChar) +
         '  LastDrop: ' + CAST(@LastDrop AS VarChar) +
         '  LosingTabs: ' + CAST(@LosingTabs AS VarChar) +
         '  MachineNumber: ' + @MachineNumber +
         '  MachineSequence: ' + CAST(@MachineSequence AS VarChar) +
         '  Payout: ' + CAST(@Payout AS VarChar) +
         '  TabsSold: ' + CAST(@TabsSold AS VarChar) +
         '  TimeStamp: ' + CAST(@TimeStamp AS VarChar) +
         '  WinningTabs: ' + CAST(@WinningTabs AS VarChar) +
         '  TicketInAmount: ' + CAST(@TicketInAmount AS VarChar) +
         '  TicketInCount: ' + CAST(@TicketInCount AS VarChar) +
         '  PromoInAmount: ' + CAST(@PromoInAmount AS VarChar) +
         '  PromoInCount: ' + CAST(@PromoInCount AS VarChar)
      EXEC InsertDebugInfo @TransID, @MsgText, @MachNbrAsInt
   END

-- Retrieve Casino information.
SELECT
   @CasinoID   = CAS_ID,
   @LocationID = LOCATION_ID
FROM CASINO
WHERE SETASDEFAULT = 1

-- Get Current Accounting Date based on Offset
SET @AcctDate = dbo.ufnGetAcctDate()

-- Update MACH_SETUP.LAST_ACTIVITY and retrieve the Game Code of the Machine.
UPDATE MACH_SETUP SET
   LAST_ACTIVITY = @TimeStamp,
   @MachGameCode = GAME_CODE
WHERE MACH_NO = @MachineNumber

-- Log transaction.
EXEC @CasinoTransID = tpInsertCasinoTrans
   @CasinoID        = @CasinoID,
   @DealNo          = @DealNo,
   @RollNo          = @RollNo,
   @TicketNo        = @TicketNo,
   @Denom           = 0,
   @TransAmt        = @TransAmt,
   @Barcode         = @Barcode,
   @TransID         = @TransID,
   @CurrentAcctDate = @AcctDate,
   @TimeStamp       = @TimeStamp,
   @MachineNumber   = @MachineNumber,
   @CardAccount     = @CardAccount,
   @Balance         = @Balance,
   @GameCode        = @MachGameCode,
   @CoinsBet        = @CoinsBet,
   @LinesBet        = @LinesBet,
   @TierLevel       = @TierLevel,
   @PressUpCount    = 0,
   @LocationID      = @LocationID,
   @MachineTimeStamp = @MachineTimeStamp

-- Insert a DROP_MACHINE record.
INSERT INTO DROP_MACHINE
   (MACH_NO, CARD_ACCT_NO, LOCATION_ID, DROP_AMOUNT, ONE_DOLLAR_AMT,
    TWO_DOLLAR_AMT, FIVE_DOLLAR_AMT, TEN_DOLLAR_AMT, TWENTY_DOLLAR_AMT,
    FIFTY_DOLLAR_AMT, HUNDRED_DOLLAR_AMT, TABS_SOLD, WINNING_TABS,
    LOSING_TABS, AMOUNT_WON, DTIMESTAMP, LAST_DROP, TICKET_IN_AMOUNT,
    TICKET_IN_COUNT, PROMO_IN_AMOUNT, PROMO_IN_COUNT)
VALUES
   (@MachineNumber, @CardAccount, @LocationID, @DropAmount, @DB1,
    @DB2, @DB5, @DB10, @DB20,
    @DB50, @DB100, @TabsSold, @WinningTabs,
    @LosingTabs, @Payout, @TimeStamp, @LastDrop, @TicketInAmount,
    @TicketInCount, @PromoInAmount, @PromoInCount)

-- Is Debug mode is on?
IF (@Debug = 1)
   -- Yes, so record outgoing argument values...
   BEGIN
      SET @MsgText =
         'tpTransD Return Values:  ' + 
         '  ErrorID: '          + CAST(@ErrorID AS VarChar) +
         '  ErrorDescription: ' + @ErrorDescription +
         '  ShutDownFlag: '     + CAST(@LockupMachine AS VarChar)
      EXEC InsertDebugInfo @TransID, @MsgText, @MachNbrAsInt
   END

-- Return results to client
SELECT
   @ErrorID          AS ErrorID,
   @ErrorDescription AS ErrorDescription,
   @LockupMachine    AS ShutDownFlag
GO
